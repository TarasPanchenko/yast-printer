/* ------------------------------------------------------------------------------
 * Copyright (c) 2006 Novell, Inc. All Rights Reserved.
 *
 *
 * This program is free software; you can redistribute it and/or modify it under
 * the terms of version 2 of the GNU General Public License as published by the
 * Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program; if not, contact Novell, Inc.
 *
 * To contact Novell about this file by physical or electronic mail, you may find
 * current contact information at www.novell.com.
 * ------------------------------------------------------------------------------
 */

/**
 * File:        include/printer/wizards.ycp
 * Package:     Configuration of printer
 * Summary:     Wizards definitions
 * Authors:     Johannes Meixner <jsmeix@suse.de>
 *
 * $Id: wizards.ycp 27914 2006-02-13 14:32:08Z locilka $
 */

{

textdomain "printer";

import "Sequencer";
import "Wizard";

include "printer/readwrite.ycp";
include "printer/basicadd.ycp";
include "printer/basicmodify.ycp";
include "printer/connectionwizard.ycp";
include "printer/printingvianetwork.ycp";
include "printer/driveroptions.ycp";
include "printer/driveradd.ycp";
include "printer/dialogs.ycp";

/**
 * Workflow of the printer configuration
 * @return sequence result
 */
any MainSequence()
{ map aliases = $[ "overview" : ``( runMainDialog() ),
                   "add" : ``( BasicAddDialog() ),
                   "add_connection_wizard" : ``( ConnectionWizardDialog() ),
                   "add_driver_add" : ``( AddDriverDialog() ),
                   "modify" : ``( BasicModifyDialog() ),
                   "modify_connection_wizard" : ``( ConnectionWizardDialog() ),
                   "modify_driver_add" : ``( AddDriverDialog() ),
                   "modify_driver_options" : ``( DriverOptionsDialog() ),
                   "printing_via_network_connection_wizard" : ``( ConnectionWizardDialog() )
                 ];
  map sequence = $[ "ws_start" : "overview",
                    "overview": $[ `abort : `abort,
                                   `back : `abort,
                                   `next : `next,
                                   `add : "add",
                                   `modify : "modify",
                                   `delete : "overview",
                                   `refresh : "overview",
                                   `printing_via_network_back : "overview",
                                   `printing_via_network_next : "overview",
                                   `printing_via_network_connection_wizard : "printing_via_network_connection_wizard",
                                   `sharing_back : "overview",
                                   `sharing_next : "overview",
                                   `policies_back : "overview",
                                   `policies_next : "overview",
                                   `autoconfig_back : "overview",
                                   `autoconfig_next : "overview"
                                 ],
                    "add" : $[ `abort : `abort,
                               `back : "overview",
                               `next : "overview",
                               `connection_wizard : "add_connection_wizard",
                               `add_driver : "add_driver_add",
                               `run_hpsetup : "overview"
                             ],
                    "add_connection_wizard" : $[ `abort : `abort,
                                                 `back : "add",
                                                 `next : "add"
                                               ],
                    "add_driver_add" : $[ `abort : `abort,
                                          `back : "add",
                                          `next : "add"
                                        ],
                    "modify" : $[ `abort : `abort,
                                  `back : "overview",
                                  `next : "overview",
                                  `connection_wizard : "modify_connection_wizard",
                                  `add_driver : "modify_driver_add",
                                  `driver_options : "modify_driver_options"
                                ],
                    "modify_connection_wizard" : $[ `abort : `abort,
                                                    `back : "modify",
                                                    `next : "modify"
                                                  ],
                    "modify_driver_add" : $[ `abort : `abort,
                                             `back : "modify",
                                             `next : "modify"
                                           ],
                    "modify_driver_options" : $[ `abort : `abort,
                                                 `back : "modify",
                                                 `next : "modify"
                                               ],
                    "printing_via_network_connection_wizard" : $[ `abort : `abort,
                                                                  `back : "overview",
                                                                  `next : "add"
                                                                ],
                  ];
  return Sequencer::Run( aliases, sequence );
}

/**
 * Whole configuration of printer
 * @return sequence result
 */
any PrinterSequence()
{ map aliases = $[ "read" : [ ``( ReadDialog() ), true ],
                   "main" : ``( MainSequence() ),
                   "write" : [ ``( WriteDialog() ), true ]
                 ];
  map sequence = $[ "ws_start" : "read",
                    "read" : $[ `abort : `abort,
                                `next : "main"
                              ],
                    "main" : $[ `abort : `abort,
                                `next : "write"
                              ],
                    "write" : $[ `abort : `abort,
                                 `next : `next
                               ]
                  ];
    Wizard::CreateDialog();
    Wizard::SetDesktopIcon("printer");
    any ret = Sequencer::Run(aliases, sequence);
    UI::CloseDialog();
    return ret;
}

/**
 * Whole configuration of printer but without reading and writing.
 * For use with autoinstallation.
 * @return sequence result
 */
any PrinterAutoSequence()
{ /* Initialization dialog caption */
  string caption = _("Printer Configuration");
  /* Initialization dialog contents */
  term contents = `Label( _("Initializing...") );
  Wizard::CreateDialog();
  Wizard::SetContentsButtons( caption,
                              contents,
                              "",
                              Label::BackButton(),
                              Label::NextButton()
                            );
  any ret = MainSequence();
  UI::CloseDialog();
  return ret;
}

/* EOF */
}
