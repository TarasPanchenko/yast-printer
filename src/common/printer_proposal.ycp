/**
 * File:
 *   clients/proposal_printer.ycp
 *
 * Package:
 *   Configuration of printer
 *
 * Summary:
 *   Proposal function dispatcher.
 *
 * Authors:
 *   Petr Blahos <pblahos@suse.cz>
 *
 * $Id$
 *
 * Proposal function dispatcher for printer configuration.
 */

{
    textdomain "printer";
    import "Printer";
    import "Printerlib";
    import "Arch";
    include "printer/misc.ycp";

    string func  = Args(0);
    map    param = Args(1);
    map    ret   = $[];

    integer spool_sys = Printerlib::getSpoolSystem ();

    if (spool_sys == 1)
	Printer::spooler = "lprng";
    else if (spool_sys == 2)
	Printer::spooler = "cups";
    if (func == "MakeProposal")
    {
	startCupsServer ();
	boolean force_reset = lookup (param, "force_reset", false);
	string proposal = "";
	string warning = nil;
	symbol warning_level = nil;
	if (1 == spool_sys || 2 == spool_sys)
	{
	    if (force_reset || !Printerlib::proposal_valid)
	    {
		Printerlib::proposal_valid = true;
		Printer::Reset ();
		if (Mode::update)
		{
		    restartSpoolerIfNeeded (true);
		    Printer::Read ();
		}
		else
		{
		    Printer::do_not_read_cups = true;
		    Printer::Read ();
		    Printer::do_not_read_cups = false;
		}
		// S390/S390x do not have local printers,
		// so no warning and no probing is necessary (#19186)
		if (Arch::s390)
		{
		    Printer::detect = false;
		}
		if (nil == Printer::detect)
		    Printer::detect = UI::AnyQuestionPopup (WarningMsg(),
			// popup
			_("During the following step, YaST2 will try to detect local printers only.\n
") +
			// popup, continuing
			_("Printer detection can now start. In some cases, detection can cause a
system freeze. If a freeze happens, select Skip Detection
when next running the printer configuration.

Detect your printers?
"), YesButtonLabel (),
		    // pushbutton
		    _("&Skip detection"), `focus_yes);

		if (Printer::detect)
		    Printer::Detect ();
		if (size (Printer::printcap) == 0
		    && size (Printer::autodetected) == 0
		    && Printer::spooler == "cups")
		{
		    Printerlib::stopServices ();
		    // no local printer found
		    y2milestone ("No local printer found, trying IPP servers");
		    string pid = nil;
		    if (SCR::Read (.target.size,
			"/var/run/listen_remote_ipp.pid") > 0)
		    {
		        pid = SCR::Read (.target.string,
			    "/var/run/listen_remote_ipp.pid");
		    }
		    boolean abort_server = false;
		    if (pid != nil)
		    {
			y2milestone ("Scanning for CUPS server in progress");
			SCR::Execute (.target.bash,
			    sformat ("kill -1 %1", pid));
			boolean runs = true;
			boolean displayed = false;
			integer counter = 0;
			while (runs)
			{
			    if (counter == 10)
			    {
				displayed = true;
				UI::OpenDialog (`VBox (`Label (_("Detecting CUPS network server...")),
`PushButton (`id (`abort), `opt (`key_F9), AbortButtonLabel ())));

			    }
			    sleep (100);
			    runs = SCR::Read (.target.size,
				"/var/run/listen_remote_ipp.pid") > 0;
			    counter = counter + 1;
                            if (displayed)
                            {
                                any ui = UI::PollInput ();
                                if (ui == `abort)
                                {
                                    SCR::Execute (.target.bash,
					sformat ("kill -15 %1", pid));
				    runs = false;
				    abort_server = true;
                                }
                            }

			}
			if (displayed)
			    UI::CloseDialog ();
		    }
		    if (SCR::Read (.target.size,
			"/var/lib/YaST2/cups_network_server_name") != -1)
		    {
			y2milestone ("File with CUPS servers found");
			if (! abort_server)
			{
			    string tmp = SCR::Read (.target.string,
				"/var/lib/YaST2/cups_network_server_name");
			    Printer::cups_servers = splitstring (tmp, "\n");
			    Printer::cups_servers
				= toset (Printer::cups_servers);
			    Printer::cups_servers = filter (`s,
				Printer::cups_servers, ``(
				s != "" && s != nil));
			}
// don't do it here because of possibility to run propose repeatedly
//			SCR::Execute (.target.bash,
//			    "/bin/rm /var/lib/YaST2/cups_network_server_name");
		    }
		    if (size (Printer::cups_servers) > 0)
		    {
			y2milestone ("Found servers: %1",Printer::cups_servers);
			string server = Printer::cups_servers[0]:"";
			Printer::cups_installation = 1;
			Printer::server_hostname = server;
		    }
		}
		// cleanup after testing of remote CUPS server existence
		SCR::Execute (.target.bash, "test -f /var/lib/YaST2/cups_network_server_name && /bin/rm /var/lib/YaST2/cups_network_server_name");
		SCR::Execute (.target.bash, "test -f /var/run/listen_remote_ipp.pid && /bin/rm /var/run/listen_remote_ipp.pid");
		Printer::Propose ();
	    }
	    if (size (Printer::printcap) > 0 && ! Printer::tested)
	    {
		ret = $[
		    // warning message (part of richtext)
		    "warning" :_("<p><a href=\"printer_conf\">Test</a> your printer configuration before proceeding.</p>"),
		    "warning_level" : `notice,
		    ];
	    }
	    proposal = Printer::Summary ([]);
	}
	else
	{
	    ret = $[
		    // warning message (part of richtext)
		"warning" : _("The print spooler is not installed properly."),
		"warning_level" : `error,
	    ];
	}
	ret = add (ret, "preformatted_proposal", proposal);
    }
    else if (func == "AskUser")
    {
	any saved = nil;
	any seq = nil;
	saved = Printer::Export ();
	seq = CallFunction (`printer (.noio));
	if (`next != seq)
	    Printer::Import (saved);
	ret = $[ "workflow_sequence" : seq, ];
    }
    else if (func == "Description")
    {
	// richtext label
	ret = $[ "rich_text_title" :	_("Printers"),
		// menu title
		 "menu_title" :		_("&Printers"),
		 "id" :			"printer_conf",
		 ];
    }
    else if (func == "Write")
    {
	// remove the result of CUPS server detection
	SCR::Execute (.target.bash,
	    "test -f /var/lib/YaST2/cups_network_server_name && /bin/rm /var/lib/YaST2/cups_network_server_name");

	/**
	 * This is rather evil but should work for this case.
	 * As proposal is called only on the fresh installation
	 * with nothing configured, we may just check if the list
	 * of printers is empty (there is nothing to save).
	 */
//	restartSpoolerIfNeeded (true);
	boolean success = true;
	if (1 == spool_sys || 2 == spool_sys)
	{
	    if (0 != size (Printer::printcap) || Printer::cupsInstallationType () == `client)
	    success = Printer::Write ();
	}
	else if (512 + 2 == spool_sys) // CUPS client-only
	{
	    success = Printer::Write ();
	}
	Printerlib::stopServices ();
        ret = $[ "success" : success ];
    }
    return ret;
}
