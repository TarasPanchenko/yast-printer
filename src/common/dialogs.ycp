/**
 * File:
 *  dialogs.ycp
 * Module:
 *  Printer configurator
 * Summary:
 *  Main dialogs.
 * Authors:
 *  Petr Blahos <pblahos@suse.cz>
 *
 * $Id$
 * Main dialogs.
 */
{
    textdomain "printer";

    import "Wizard_hw";
    import "Printerlib";
    import "Printer";
    import "Arch";

    include "printer/misc.ycp";
    include "printer/helps.ycp";
    include "ui/common_popups.ycp";
    include "ui/file_popups.ycp";
    include "wizard/sequencer.ycp";

    include "printer/dialogs-cups.ycp";
    include "printer/dialogs-lprng.ycp";


    // popup
    string __tmp__ = _("Detecting CUPS server...");

    // popup
    __tmp__ = _("Looking for CUPS server...");

    /**
      * for temporary storing of printer model
      * because of `back button
      */
    global map tmp_printer_manuf_model = $[];

    /**
      * Ask user to install requested packages
      * @param packages list of package names
      * @return boolean true if wants to install
      */
    global define boolean AskInstallPackages (list packages) ``{
	// yes-no popup, %1 is list of package names
	string popup = sformat (_("For the selected function, the additional packages
%1
must be installed.

Install them now?
"), mergestring (packages, ", "));
	return UI::YesNoPopup (popup);
    }

    /**
      * Casts value type to integer if you know what I am talking about.
      * @param a value
      * @return integer representation of a
      */
    global define integer AnyToInteger (any a) ``{
	integer c = a;
	return c;
    }
    /**
      * Disk file specification
      * @param type string "file" or "pipe"
      * @return symbol `next or `back
      */
    global define symbol runDiskFileDialog (string type) ``{
	//
	// parse Uri
	//
	string file = lookup (Printer::printer, "uri", "");
	if ("" != file && nil != file)
	{
	    // file must start with "file:"
	    if ( "file:" != substring (file, 0, 5) && "pipe:" != substring (file, 0, 5))
	    {
		file = "";
	    }
	    else
	    {
		file = substring (file, 5);
		while ("/" == substring (file, 0, 1))
		{
		    file = substring (file, 1);
		}
		file = "/" + file;
	    }
	}
	term contents = `HBox (
	    `HSpacing (8),
	    `VBox (
		// frame label
		`Frame ( _("Connection"),
			 `VBox (
			     `HBox (
				 `HSpacing (0.7),
				 // text entry label
				 `TextEntry (`id(`name), (type == "file" ? _("&File name:")
				 // text entry label
				 : _("&Program name:")), file),
				 `HSpacing (0.7)
				 ),
			     `VSpacing (0.5)
			     )
		    )
		),
	    `HSpacing (8)
	    );
	// dialog label
	Wizard::SetContentsButtons (type == "file" ? _("Printing to file")
	// dialog label
	: _("Printing to pipe"), contents, getDiskFileHelp (), BackButtonLabel (), NextButtonLabel ());

	symbol ret = nil;
	while (true)
	{
	    ret = UI::UserInput ();
	    if (ret == `cancel)
		ret = `abort;
	    if (`next == ret)
	    {   // get the file name
		string name = UI::QueryWidget (`id (`name), `Value);
		if (size (name) > 0)
		{
		    if ( "/" != substring (name, 0, 1))
		    {
			name = "/" + name;
		    }
		    file = (type == "file" ? "file:" : "pipe:") + name;
		    Printer::printer = add (Printer::printer, "uri", file);
		    break;
		}
		else
		{
		    // message box
		    UI::MessagePopup (type == file ? _("Enter the full path of the file to which to print.")
		    // message box
			: _("Enter full path of the program to which to print through pipe."));
		}
	    }
	    else if (`back == ret)
	    {
		break ;
	    }
	    else if (`abort == ret)
	    {
		if (reallyAbort ())
		{
		    break;
		}
	    }
	}
	return ret;
    }
    /**
     * Asks user about settings of selected device (port number, other settings
     * for serial ports). Lets user do test of device. Uses global variable
     * printer.
     * @param type "parallel", "serial", "usb"
     *
     * @return symbol `back, `next, `abort
     */
    global define symbol runDeviceDialog (string type) ``{
	boolean problematic_usb = false;
	string uri = lookup (Printer::printer, "tmpuri", "");
	if ("" == uri)
	    uri = lookup (Printer::printer, "uri", "parallel");
	if ("" == type)
	{
	    type = Printerlib::getUriType ("uri");
	}
	string device = Printerlib::getUriDevice (uri);
	list devices = [];
	path read_devs = "parallel" == type ? .proc.parport.devices : "usb" == type ? .proc.usblp.devices : "irda" == type ? .proc.irlpt.devices : .proc.serial.devices;
	integer baudrate = Printerlib::getUriBaudrate (uri);
	string ser_settings = Printerlib::getUriValue (uri, "ty");
	integer i = find (device, "?");
	if (0 == baudrate && -1 == Printer::index)
	    baudrate = 9600;
	if (nil != i)
	    device = substring (device, 0, i);
	if ("irda" != type)
	    devices = SCR::Read (read_devs);
	else
	    devices = [];

	if (type == "usb" && Printer::spooler == "cups")
	{
	    Printerlib::setCupsUsbDevicesInfo ();
	    devices = Printerlib::getCupsUsbDevices ();
	    if (size (devices) != size (toset (devices)))
	    {
		problematic_usb = true;
		list problematic = Printerlib::getProblematicCupsUsbDevices ();
	        devices = maplist (`d, Printerlib::cups_usb_devices, ``{
		    if (contains (problematic, d))
		    if (true)
		    {
			return d[2]:"";
		    }
		    else
		    {
			return d[0]:"";
		    }
		});
	    }
	}

	if (size (devices) == 0)
	{
	    if (type == "parallel")
	    {
		// popup CAUTION: "device" here means /dev/lp0, /dev/lp1, ...
		UI::MessagePopup (_("No parallel devices (/dev/lp?) found. It seems
that your parallel port is not properly configured.
"));
		devices = add (devices, "/dev/lp0");
	    }
	    else if (type == "usb")
	    {
		// popup CAUTION: "device" here means /dev/usb/lp0, /dev/usb/lp1, ...
		UI::MessagePopup (_("No USB devices (/dev/usb/lp?) found. It seems
that your USB bus is not properly configured.
"));
		devices = add (devices, "/dev/usb/lp0");
	    }
	    else if (type == "serial")
	    {
		// popup CAUTION: "device" here means /dev/ttyS0, /dev/ttyS1, ..
		UI::MessagePopup (_("No serial devices (/dev/ttyS?) found. It seems
that your serial ports are not properly configured.
"));
		devices = add (devices, "/dev/ttyS0");
	    }
	    else if (type == "irda")
	    {
		devices = ["/dev/irlpt0", "/dev/irlpt1", "/dev/irlpt2", "/dev/irlpt3"];
	    }
	}
	if (type != Printerlib::getUriType (device))
	    device = "";
	if (device == "")
	    device = select (devices, 0, "");
	list device_val = getDevicesItems (devices, device);
	term details = `VBox (
	    // PushButton
	    `HBox (`HStretch (), `PushButton (`id (`details), _("&Other...")))
	);

	if ("parallel" == type || "serial" == type)
	{
	    if (!auto_mode)
	    {
		details = add (details,
		    `HBox (`HStretch (), `PushButton (`id ("parallel" == type ? `ppdetails : `spdetails), `opt (`key_F2),
			// PushButton
			"parallel" == type ? _("&Parallel port details...")
			// PushButton
			    : _("&Serial port details..."))));
	    }
	}

	term contents =
	    `VBox (`VSpacing (2),
		   `HBox (`HSpacing (5),
			  // frame
			  `Frame (_("Connection"),
				  `HBox (`HSpacing (0.7),
					 `VBox (`VSpacing (0.2),
						`ReplacePoint (
						    `id (`devicesel_replace),
						    `SelectionBox (
							`id (`devicesel),
							`opt (`notify),
							// Unix device /dev/(lp|usb/lp|ttyS)*
							_("Select the d&evice:"), device_val)
						    ),
						details,
						`VSpacing (0.2)),
					 `VSpacing (6),  // Vert. size of the selection box...
					 `HSpacing (0.7))),
			  `HSpacing (5)),
		   `VStretch (),
		    // pushbutton
		   `PushButton (`id (`test), `opt (`key_F6), _("&Test printer connection")),
		   `VStretch ()
		);
	Wizard::SetContentsButtons (getAskDeviceLabel (type), contents, getAskDeviceHelp (type), BackButtonLabel (), NextButtonLabel ());

	if (problematic_usb)
	{
	    if (Printer::conf_detected)
	    {
		// message popup
		UI::MessagePopup (_("Warning!

USB printer detection finds at least two printers
that have the same identification.
You must select and configure them manually.
Do the \"Test\" to make sure which printer is
actually selected.
"));

	    }
	    else
	    {
		// message popup
		UI::MessagePopup (_("Warning!

USB printer detection finds at least two printers
that have the same identification.
Do the \"Test\" to make sure which printer is
actually selected.
"));
	    }
	}

	symbol ret = nil;
	repeat {
	    ret = UI::UserInput ();
            if (ret == `cancel)
                ret = `abort;
	    device = UI::QueryWidget (`id (`devicesel), `CurrentItem);

/*	    if (type == "serial")
	    {
		ser_settings = UI::QueryWidget (`id (`ser_settings), `Value);
		string tmp = UI::QueryWidget (`id (`baudent), `Value);
		baudrate = tointeger (tmp);
		if (`next == ret && !testBaudRate (baudrate))
		    ret = nil;
	    }*/

	    if (ret == `test)
	    {
		Printerlib::testDevice (device);
	    }
	    else if (`abort == ret || `cancel == ret)
	    {
		ret = reallyAbort () ? `abort : nil;
	    }
	    else if (`details == ret)
	    {
		string device_temp = getDeviceName (device);
		if ("" != device_temp)
		{
		    device = device_temp;
		    device_val = getDevicesItems (devices, device);
		    UI::ReplaceWidget (
			`id (`devicesel_replace),
			`SelectionBox (
			    `id (`devicesel),
			    `opt (`notify),
			    // Unix device /dev/(lp|usb/lp|ttyS)*
			    _("Select the d&evice:"), device_val)
			);
		}
	    }
	} until (ret == `abort || ret == `back || ret == `next || `ppdetails == ret || `spdetails == ret);
	if (`spdetails == ret)
	{
	    uri = type + ":" + device;
	    if ("serial" == type)
		uri = uri + sformat ("?baudrate=%1+ty=%2", baudrate, ser_settings);
	    Printer::printer = add (Printer::printer, "tmpuri", uri);
	}
	else
	{
	    Printer::printer = filter (`k, `v, Printer::printer, ``(k != "tmpuri"));
	}
	if (`next == ret)
	{
	    uri = type + ":" + device;
	    if ("serial" == type)
		uri = uri + sformat ("?baudrate=%1+ty=%2", baudrate, ser_settings);
	    Printer::printer = add (Printer::printer, "uri", uri);
	}
	return ret;
    }
    /**
     * Connection details for prefilter queue
     * @return symbol `next, `back, `abort
     */
    global define symbol runPrefilterDialog () ``{

	boolean supported = testSupportedQueue ("prefilter");
        if (! supported)
        {
            return `back;
        }

	string uri = lookup (Printer::printer, "uri", "");
	boolean create_queue = false;
	string remote_printer = lookup (Printer::printer, "remote_printer", "");
	string queue = Printerlib::getUriRemoteQueue (uri);
	string hostname = lookup (Printer::printer, "remote_host", "");
	list all_forwarding = getForwardingQueues (queue);

	create_queue = lookup (Printer::printer, "create_remote_queue", true);
	remote_printer = lookup (Printer::printer, "remote_printer", "");

	// push button label
	term test_button = Printer::isNetAvailable () ? `PushButton (`id (`test), `opt (`key_F6), _("&Test printer connection")) : `VSpacing (1);
	term queue_button = `HSpacing (0);
	term queue_entry = `TextEntry (`id (`queueent),
					  // text entry label
					  _("Name of the remote &queue:"),
					    remote_printer);
	// menubutton
	term lookup_hosts = `MenuButton (_("&Lookup"), [
			// menuentry
		    `item (`id (`get_hosts_lpd), _("&LPD servers")),
			// menuentry
		    `item (`id (`get_hosts), _("&All hosts")),
		]);

	term contents = `RadioButtonGroup (`id (`prefilter_rbgroup),
		`HBox (`HSpacing (8.0),
		    `VBox (`VStretch (),
			// Frame around hostname, queue, (user, passwd)
			`Frame (_("Connection information"),
			    `HBox (`HSpacing (0.7), `VSquash (
				    `VBox (`VBox (`Left (`RadioButton (`id (`old_forwarding), `opt (`notify),
									    // radio button label
									    _("U&se Configured TCP/IP Forwarding Queue:"),
									    !create_queue)),
						       `HBox (`HSpacing (3.5),
									// selectionbox
							      `SelectionBox (`id (`all_forwarding_sb), _("Q&ueues:"), all_forwarding)),
						       `VSpacing (0.5),
						       `Left (`RadioButton (`id (`new_forwarding), `opt (`notify),
							// radio button label
							_("&Create a New One:"), create_queue))),
						`HBox (`HSpacing (3.5),
						       `VBox (
							   `HBox (
							       `ReplacePoint (
								   `id (`hostent_replace),
								   `ComboBox (`id (`hostnameent),
									      `opt (`editable, `hstretch),
									      // combo box label
									      _("&Host name of the printer:"))),
								   //Label: Lookup hosts on network
							       `VBox (`VStretch (),
								      lookup_hosts)
							       ),
							   `HBox (
							       `ReplacePoint (
								   `id (`queueent_replace),
								   queue_entry),
							       queue_button))),
						`VSpacing (0.5))),
					    `HSpacing (0.7))),
			     `VStretch (),
			     test_button,
			     `VStretch ()),
		      `HSpacing (8.0)));

	// button label
	Wizard::SetContentsButtons (_("Prefilter queue for remote printers"), contents, getAskRemoteHelp ("prefilter"), BackButtonLabel (), NextButtonLabel ());
	UI::ChangeWidget (`id (`hostnameent), `Value, hostname);

	symbol ret = nil;
	repeat {
	    UI (``{
		if (QueryWidget (`id (`prefilter_rbgroup), `CurrentButton) == `new_forwarding)
		{
		    ChangeWidget (`id (`all_forwarding_sb), `Enabled, false);
		    ChangeWidget (`id (`hostnameent), `Enabled, true);
		    ChangeWidget (`id (`queueent), `Enabled, true);
		    ChangeWidget (`id (`test), `Enabled, true);
		}
		else
		{
		    ChangeWidget (`id (`all_forwarding_sb), `Enabled, true);
		    ChangeWidget (`id (`hostnameent), `Enabled, false);
		    ChangeWidget (`id (`queueent), `Enabled, false);
		    ChangeWidget (`id (`test), `Enabled, false);
		}
	    });

	    ret = UI::UserInput ();
            if (ret == `cancel)
                ret = `abort;
	    hostname = UI::QueryWidget (`id (`hostnameent), `Value);
	    queue = UI::QueryWidget (`id (`queueent), `Value);
	    create_queue = (UI::QueryWidget (`id (`prefilter_rbgroup),
					      `CurrentButton) == `new_forwarding);

	    remote_printer = create_queue ? "" : UI::QueryWidget (`id (`all_forwarding_sb), `CurrentItem);
	    if (ret == `next)
	    {
		if (!create_queue && !checkRemotePrinter (remote_printer))
		    ret = nil;
		else if (create_queue && (!checkHostName (hostname) || !checkQueueName (queue)))
		    ret = nil;
	    }
	    if (`get_hosts == ret || `get_hosts_lpd == ret)
	    {
		// Find the hosts...
		list hosts = [];
		UI::OpenDialog (`opt (`decorated),
				// label
				 `Label (_("Scanning for hosts on this LAN...")));
		if (`get_hosts_lpd == ret)
		    hosts = SCR::Read (.net.hostnames, 515);
		else
		    hosts = SCR::Read (.net.hostnames);
		UI::CloseDialog ();
		if (hosts == nil)
		{
		    hosts = [];
		}
		UI::ReplaceWidget (
		    `id (`hostent_replace),
		    // combobox label
		    `ComboBox (`id (`hostnameent), `opt (`editable, `hstretch), _("&Host name of the printer:"), sort (hosts))
		    );
	    }
	    if ((ret == `test) && checkHostName (hostname) && checkQueueName (queue))
	    {
		Printerlib::testRemote (hostname, queue, "prefilter", 0);
	    }
	    if (`abort == ret || `cancel == ret)
	    {
		ret = reallyAbort () ? `abort : nil;
	    }
	} until (ret == `abort || ret == `back || ret == `next);

	if (ret == `next)
	{
	    Printer::printer = add (Printer::printer, "create_remote_queue", create_queue);
	    if (create_queue)
	    {
		Printer::printer = add (Printer::printer, "remote_printer", queue);
		Printer::printer = add (Printer::printer, "remote_host", hostname);
		// remote_printer;
	    }
	    Printer::printer = add (Printer::printer, "uri", "prefilter://" + remote_printer);
	}
	return ret;
    }
    /**
     * Asks user about settings of selected type of remote pritner. E.g. hostname,
     * username+password for samba, ncp, ...
     * @param type one of "samba", "novell", "lpd"
     * @return symbol `next, `back, `abort
     */
    global define symbol runRemoteDialog (string type) ``{
	string uri = lookup (Printer::printer, "uri", "");
	string user = "";
	string password = "";
	string queue = type == "ipp" ? "printers/" : "";
	string hostname = "";
	integer port = 0;

	// check necessary packages
	if (type == "samba")
	{
            if (Mode::config)
            {
                // TODO add to requested list
            }
            else if (! Pkg::IsProvided ("samba-client")
		&& AskInstallPackages (["samba-client"]))
	    {
		import "PackageCallbacks";
		Pkg::DoProvide (["samba-client"]);
		Pkg::PkgSolve ();
		Pkg::PkgCommit (0);
		WFM::CallModule("inst_suseconfig", []);
	    }
	}
	else if (type == "novell")
	{
            if (Mode::config)
            {
                // TODO add to requested list
            }
            else if (! Pkg::IsProvided ("ncpfs")
		&& AskInstallPackages (["ncpfs"]))
            {
		import "PackageCallbacks";
		Pkg::DoProvide (["ncpfs"]);
		Pkg::PkgSolve ();
		Pkg::PkgCommit (0);
		WFM::CallModule("inst_suseconfig", []);
	    }
	}

	boolean supported = testSupportedQueue (type);
	if (! supported)
	{
	    return `back;
	}

	boolean has_user_password = (type == "samba" || type == "novell");

	// label
	string hent_str = _("&Host name of the printer server:");
	term uspas = `VSpacing (0.2);

	integer i = findlastof (uri, "/");
	if (nil != i)
	{
	    queue = Printerlib::getUriRemoteQueue (uri);
	    hostname = Printerlib::getUriHost (uri);
	}
	if (has_user_password)
	{
	    list l = Printerlib::getUriUsernamePass (uri);
	    user = select (l, 0, "");
	    password = select (l, 1, "");
	    uspas = `VSquash (`VBox (
		// text entry label
		`TextEntry (`id (`userent), _("&User:"), user),
		// text entry label
		`Password (`id (`passwordent), _("&Password:"), password)));
	}

	// push button label
	term test_button = Printer::isNetAvailable () ? `PushButton (`id (`test), `opt (`key_F6), _("&Test remote LPD access")) : `VSpacing (1);
	term queue_button = `HSpacing (0);
	term queue_entry = `TextEntry (`id (`queueent),
					  // text entry label
					  _("Name of the remote &queue:"),
					  queue);
	// menu button
	term lookup_hosts = `MenuButton (_("&Lookup"), [
			// menuentry
		    `item (`id (`get_hosts_special), _("&LPD servers")),
			// menuentry
		    `item (`id (`get_hosts), _("&All hosts")),
		]);
	if (type == "novell")
	    {
		// menu button
		lookup_hosts = `PushButton (`id (`get_hosts), _("&Lookup"));
		// pushbutton
		test_button = Printer::isNetAvailable () ? `PushButton (`id (`test), `opt (`key_F6), _("&Test remote Novell access")) : `VSpacing (1);
	    }
	if (type == "samba")
	    {
		// pushbutton
		test_button = Printer::isNetAvailable () ? `PushButton (`id (`test), `opt (`key_F6), _("&Test remote SMB access")) : `VSpacing (1);
		queue_entry = `ComboBox (`id (`queueent), `opt (`editable, `hstretch),
					    // text entry label
					    _("Name of the remote &queue:"));
		// pushbutton
		queue_button = `VBox (`VStretch (), `PushButton (`id (`get_printers), _("L&ookup")));
		// menu button
		lookup_hosts = `MenuButton (_("&Lookup"), [
			// menuentry
		    `item (`id (`get_hosts_special), _("&Samba servers")),
			// menuentry
		    `item (`id (`get_hosts), _("&All hosts")),
		]);
	    }
	if (type == "ipp")
	{
	    // pushbutton
	    test_button = Printer::isNetAvailable () ? `PushButton (`id (`test), `opt (`key_F6), _("&Test remote IPP access")) : `VSpacing (1);
	    queue_entry = `ComboBox (`id (`queueent), `opt (`editable, `hstretch),
                                            // text entry label
                                            _("Name of the remote &queue:"));
					    // push button
	    queue_button = `VBox (`VStretch (), `PushButton (`id (`get_printers), _("L&ookup")));
	    // menu button
	    lookup_hosts = `MenuButton (_("&Lookup"), [
		// menu item
                `item (`id (`get_hosts_special), _("&IPP servers")),
		// menu item
                `item (`id (`get_hosts), _("&All hosts")),
            ]);
	}
	if (type == "socket")
	{
	    port = Printerlib::getUriPort (uri);
	    if (port == 0)
		port = 9100;
	    // push button
	    test_button = Printer::isNetAvailable () ? `PushButton (`id (`test), `opt (`key_F6), _("&Test remote socket access")) : `VSpacing (1);
	    // text entry label
	    queue_entry = `VBox (`VStretch (), `TextEntry (`id (`port), _("&TCP port number"), sformat("%1", port)));
	    // menu button
	    lookup_hosts = `MenuButton (_("&Lookup"), [
		// menu item
		`item (`id (`get_hosts_special), _("&Direct socket servers")),
		// menu item
		`item (`id (`get_hosts), _("&All hosts")),
	    ]);
	}

	term contents = `HBox (`HSpacing (8.0),
	      `VBox (`VStretch (),
		     // Frame around hostname, queue, (user, passwd)
		     `Frame (_("Connection information"),
			     `HBox (`HSpacing (0.7),
				    `VSquash (`VBox (
					`HBox (`HSpacing (0.5),
					       `VBox (
						   `HBox (
						       `ReplacePoint (
							   `id (`hostent_replace),
							   `ComboBox (`id (`hostnameent),
								      `opt (`editable, `hstretch),
								      hent_str)),
							   //Label: Lookup hosts on network
						       `VBox (`VStretch (),
							      lookup_hosts)
						       ),
						   `HBox (
						       `ReplacePoint (
							   `id (`queueent_replace),
							   queue_entry),
						       queue_button))),
							uspas,
					`VSpacing (0.5))),
				    `HSpacing (0.7))),
		     `VStretch (),
		     test_button,
		     `VStretch ()
		     ),
	      `HSpacing (8.0));

	// button label
	Wizard::SetContentsButtons (getAskDeviceLabel (type), contents, getAskRemoteHelp (type), BackButtonLabel (), NextButtonLabel ());

	if (type != "socket")
	    UI::ChangeWidget (`id (`queueent), `Value, queue);
	else
	    UI::ChangeWidget (`id (`port), `ValidChars, "1234567890");
	UI::ChangeWidget (`id (`hostnameent), `Value, hostname);

	symbol ret = nil;
	repeat {
	    ret = UI::UserInput ();
            if (ret == `cancel)
                ret = `abort;
	    hostname = UI::QueryWidget (`id (`hostnameent), `Value);
	    if (type != "socket")
		queue = UI::QueryWidget (`id (`queueent), `Value);
	    else
	    {
		string tmp = UI::QueryWidget (`id (`port), `Value);
		while (substring (tmp, 0, 1) == "0")
		    tmp = substring (tmp, 1);
		if (tmp == "")
		    tmp = "9100";
		port = tointeger (tmp);
	    }
	    if (has_user_password)
		{
		    user = UI::QueryWidget (`id (`userent), `Value);
		    password = UI::QueryWidget (`id (`passwordent), `Value);
		}
	    if (`get_hosts == ret || `get_hosts_special == ret)
	    {
		// get remote hosts (we always offer list of hosts)
                list hosts = [];
		string save_type = type;
		if (`get_hosts_special == ret && type == "socket")
		{
		    type = UI::QueryWidget (`id (`port), `Value);
		    if (type == "0" || type == "")
			type = "9100";
		}
                hosts = getHostnames (`get_hosts_special == ret ? type : "");
		type = save_type;
		UI::ReplaceWidget (
		    `id (`hostent_replace),
		    `ComboBox (`id (`hostnameent), `opt (`editable, `hstretch), hent_str, sort (hosts))
		    );
	    }
	    if (`get_printers == ret && checkHostName (hostname))
	    {
		list queues = [];
		if ("samba" == type)
		{
		    queues = SCR::Read (.smb.queues, hostname);
		}
		else if ("ipp" == type)
		{
		    queues = maplist (`i, SCR::Read (.cups.remote, hostname), ``{
			if ("ipp://" == substring (i, 0, 6))
                            i = substring (i, 6);
                        else if ("http://" == substring (i, 0, 7))
                            i = substring (i, 7);
                        while ("/" != substring (i, 0, 1) && size (i) > 0)
                            i = substring (i, 1);
			while ("/" == substring (i, 0, 1) && size (i) > 0)
			    i = substring (i, 1);
                        return i;
                    });
		}
		queue_entry = `ComboBox (`id (`queueent), `opt (`editable, `hstretch),
                    // text entry label
                    _("Name of the remote &queue:"), queues);
                UI::ReplaceWidget (`id (`queueent_replace), queue_entry);
	    }
	    if (ret == `test && checkHostName (hostname) && (type == "socket" || checkQueueName (queue)) && (!has_user_password || ( checkUserName (user) && checkPassword (password))))
	    {
		string test_queue = "";
		if (type == "ipp" && "printers/" == substring (queue, 0, 9))
		    test_queue = substring (queue, 9);
		else
		    test_queue = queue;
		if ("samba" == type || "novell" == type)
		{
		    Printerlib::testRemoteUP (hostname, test_queue, user, password, type);
		}
		else
		    Printerlib::testRemote (hostname, test_queue, type, port);
	    }
	    if (`abort == ret || `cancel == ret)
	    {
		ret = reallyAbort () ? `abort : nil;
	    }
	} until (ret == `abort || ret == `back || ret == `next && checkHostName (hostname) && (type == "socket" || checkQueueName (queue))
	     && (!has_user_password || ( checkUserName (user) && checkPassword (password))));
	uri = type + "://";
	uri = uri + (has_user_password ? user + ":" + password + "@" : "");
	uri = uri + hostname;
	if (type == "ipp" && "printers/" != substring (queue, 0, 9))
	{
	    string tmp = queue;
	    while (substring (tmp, 0, 1) == "/")
		tmp = substring (tmp, 1);
	    if (filterchars (tmp, "/") == "")
		queue = "printers/" + tmp;
	}
	queue = substring (queue, 0, 1) == "/" ? queue : "/" + queue;
	uri = uri + (type == "socket" ? ":" + port : queue);
	if (ret == `next)
	{
	    Printer::printer = add (Printer::printer, "uri", uri);
	}
	return ret;
    }
    /**
     * Choose one of remote printer (fwd queues).
     * @return symbol for wizard seq.
     */
    global define symbol runChooseRemoteDialog () ``{
	string uri = lookup (Printer::printer, "uri", "");
	integer i = findlastof (uri, "/");
	string queue = (nil == i ? "" : Printerlib::getUriRemoteQueue (uri));
	list all_forwarding = getForwardingQueues (queue);
	term contents =
	    `HBox (`HSpacing (8.0),
		   `VBox (`VStretch (),
			  `VBox (
			      `Frame (
				  // frame label
				  _("Select forwarding queue"),
				  `HBox (
				      `HSpacing (0.7),
				      `VSquash (
					  `VBox (
					      // Additional widgets for the prefilter queue
					      `VBox (
						  `HBox (
						      `HSpacing (0.5),
						      // selection box label
						      `SelectionBox (`id (`all_forwarding_sb), _("&Queues:"), all_forwarding),
						      `VSpacing (8)),
						  `VSpacing (0.5)
						  )
					      )),
				      `HSpacing (0.7))),
			      `VSpacing (1)),
			  `VStretch ()),
		   `HSpacing (8.0));

	// dialog label
	Wizard::SetContentsButtons (_("Prefilter queue for remote printers"), contents, getChooseRemoteHelp (), BackButtonLabel (), NextButtonLabel ());
	symbol ret = nil;
	while (`back != ret && `abort != ret && `next != ret) {
	    ret = UI::UserInput ();
            if (ret == `cancel)
                ret = `abort;
	    if (`next == ret)
		{
		    string selected = UI::QueryWidget (`id (`all_forwarding_sb), `CurrentItem);
		    if (nil == selected)
		    {
			ret = nil;
			// message box
			UI::MessagePopup (_("Select a queue from the list."));
		    }
		    else
		    {
			Printer::printer = add (Printer::printer, "uri", "prefilter://" + selected);
		    }
		}
	    if (`abort == ret || `cancel == ret)
		{
		    ret = reallyAbort () ? `abort : nil;
		}
	}
	return ret;
    }
    /**
     * direct Uri definition
     * @return symbol `next or `back
     */
    global define symbol runUriDialog ()``{
	//
	// parse Uri
	//
	string device = lookup (Printer::printer, "Uri", "");
	term contents = `HBox (
	    `HSpacing (8),
	    `VBox (
		// frame label
		`Frame ( _("Connection"),
			 `VBox (
			     `HBox (
				 `HSpacing (0.7),
				  // textentry label
				 `TextEntry (`id(`name), _("&Uri:"), device),
				 `HSpacing (0.7)
				 ),
			     `VSpacing (0.5)
			     )
		    ),
		`VSpacing (2)
		),
	    `HSpacing (8)
	    );
	// dialog caption
	Wizard::SetContentsButtons (_("Printer device"), contents, getUriHelp (), BackButtonLabel (), NextButtonLabel ());

	symbol ret = nil;
	while (true)
	{
	    ret = UI::UserInput ();
            if (ret == `cancel)
                ret = `abort;
	    if (`next == ret)
	    {   // get the file name
		string name = UI::QueryWidget (`id (`name), `Value);
		Printer::printer = add (Printer::printer, "Uri", name);
		break ;
	    }
	    else if (`back == ret)
	    {
		break ;
	    }
	    else if (`abort == ret)
	    {
		if (reallyAbort ())
		{
		    break;
		}
	    }
	}
	return ret;
    }

    /**
      * Display printer information popup
      * @param info map containing info about printer from SuSE database
      * @param vdb string database key of printer vendor
      * @param mdb string database key of printer model
      */
    global define void DisplayPrinterPopup (map info, string vdb, string mdb)``{

                    string manuf_info = lookup (info, "vendor",
			// richtext
                        _("<P>No information available.</P>"));
                    string model_info = lookup (info, "printer",
                        lookup (info, "config",
			// richtext
                        _("<P>No information available.</P>")));
                    // Manufacturer: ...info... Model: ...info...  1/2
                    string info_text = _("<P><B>Manufacturer:</B></P>")
                       + manuf_info +
                        // Manufacturer: ...info... Model: ...info...  2/2
                        _("<P><B>Model:</B></P>") + model_info;
                    integer type = 0;
		    if (Printer::db == `foomatic)
			Printer::foomatic[vdb, mdb, `support]:1;

                    if (type != 2 && Printer::db == `foomatic
                        && size (filter (`k, `v,
                            Printer::foomatic[vdb, mdb]:$[],
                            ``(is (k, string)))) == 0)
                    {
                        // part of richtext
/*                        info_text = _("<p><B>This printer can be configured
with the SuSE database of printers only.</B></P>
") + info_text;*/
                    }
                    else if (type == 2)
                        // part of richtext
                        info_text = _("<p><B>This printer is not supported by SuSE Linux.</B></P>") + info_text;

                    // message box caption
                    UI::LongTextPopup (_("Printer information"),
                        `RichText (info_text), 50, 18);


    }

    /**
     * Ask about the manufacuturer and the model of the printer.
     * Uses global printer
     * @return symbol `back, `next, `abort
     */
    global define symbol runManufModelDialog () ``{
	string manuf_db = lookup (Printer::printer, "vendor_db", "");
	string model_db = lookup (Printer::printer, "device_db", "");
	string manuf = lookup (Printer::printer, "vendor", "");
	string model = lookup (Printer::printer, "device", "");
	string manuf_ieee = lookup (Printer::printer, "vendor_ieee", "");
	string model_ieee = lookup (Printer::printer, "device_ieee", "");
	if (Printer::edit_seq)
	{
	    if (tmp_printer_manuf_model == $[])
	    {
		y2debug ("tmp Storing printer %1", Printer::printer);
		tmp_printer_manuf_model = eval (Printer::printer);
	    }
	}
	map info = $[];
	boolean info_instead_next = false;

	if (Printer::db == `foomatic)
	    loadFoomaticIfNeeded ();

	// selection box label
	term manufsel = `SelectionBox (`id (`manufsel), `opt (`notify),
		// selection boc title
	    _("&Select manufacturer:"),
	    Printer::db == `foomatic
// FIXME vendor labels
		? maplist (`k, `v, Printer::foomatic, ``(k))
		: SCR::Read (.printerdb.vendors, [ manuf, manuf_ieee ]));

	list model_val = [];
	term modelsel = nil;
	term contents =
	    `VBox (`HBox (`HWeight (1, manufsel),
			  `HWeight (1, `ReplacePoint (`id (`modelsel_rep), `HSpacing ()))),
		   `HBox (
			`HStretch (),
		   // pushbutton
			  `PushButton (`id (`useppd), _("Use non-YaST2 &ppd file")),
			  `HStretch (),
		   // Button which provides information about selected manufacturer/model
			`PushButton (`id (`infobut), `opt (`key_F2), _("&Info")),
			`HStretch ()
		));
	// dialog box label
	Wizard::SetContentsButtons (_("Manufacturer and model of the printer"),
	    contents, getManufModelHelp (), BackButtonLabel (),
	    NextButtonLabel ());

	if (Printer::db == `foomatic)
	{
	    if (manuf_db == "" || manuf_db == nil)
	    {
		foreach (`k, `v, Printer::foomatic, ``{
		    if (manuf_db == "" || manuf_db == nil)
			manuf_db = k;
		});
	    }
	    UI::ChangeWidget (`id (`manufsel), `CurrentItem, manuf_db);
	}

	symbol ret = `manufsel;
	while (`abort != ret && `back != ret && ret != `next && ret != `useppd)
	{
	    if (ret == `infobut || ret == `infonext)
	    {
		if (
                (Printer::db == `foomatic &&(model_db == "" || model_db == nil))
                || (Printer::db != `foomatic && (model == "" || model == nil))
                )
	        {
	            // popup
	            UI::MessagePopup (_("Select a printer model."));
		    UI::SetFocus (`id (`modelsel));
	        }
		else
		{
		    DisplayPrinterPopup (info, manuf_db, model_db);
		}
	    }
	    if (ret == `manufsel)
	    {
		y2debug ("manufsel changed");
		if (Printer::db == `foomatic)
		{
		    if (manuf_db
			!= UI::QueryWidget (`id (`manufsel), `CurrentItem))
		    {
			manuf_db
			    = UI::QueryWidget (`id (`manufsel), `CurrentItem);
			model_db = "";
		    }
		}
		else
		{
		    list temp = UI::QueryWidget (`id (`manufsel), `CurrentItem);
		    manuf = select (temp, 0, "");
		    manuf_ieee = select (temp, 1, "");
		}
		map modnames = $[];
		if (Printer::db == `foomatic)
		{
		    map models = filter (`k, `v,
			Printer::foomatic[manuf_db]:$[],
			``(is (k, string)));
		    foreach (`k, `v, models, ``{
			string l = v[`label]:k;
			boolean add_blank = true;
			while (haskey (modnames, l))
			{
			    y2warning ("Printer label %1:%2 doubled", manuf_db,
				v[`label]:"");
			    if (add_blank)
				l = l + " ";
			    l = l + "I";
			    add_blank = false;
			}
			modnames[toupper (l)] = k;
		    });

		}

		modelsel = `SelectionBox (`id (`modelsel), `opt (`notify),
					  // selection box caption
					  _("Select &model:"),
		    Printer::db == `foomatic

			? maplist (`k, `v, modnames, ``{
			    string cur_label
				= Printer::foomatic[manuf_db, v, `label]:k;
			    if (substring (cur_label, 0, 7) == "DESKJET")
			    {
				cur_label
				    = "DeskJet" + substring (cur_label, 7);
			    }
                            else if (substring (cur_label, 0, 8) == "LASERJET")
                            {
                                cur_label
                                    = "LaserJet" + substring (cur_label, 8);
                            }
                            else if (substring (cur_label, 0, 9) == "OFFICEJET")
                            {
                                cur_label
                                    = "OfficeJet" + substring (cur_label, 9);
                            }
                            else if (substring (cur_label, 0, 10)
				== "PHOTOSMART")
                            {
                                cur_label
                                    = "PhotoSmart" + substring (cur_label, 10);
                            }

			    return `item (`id (v), cur_label);
			})
			: SCR::Read (add (.printerdb.models, manuf),
			    [ model, model_ieee ]));
		UI::ReplaceWidget (`id (`modelsel_rep), modelsel);
		if (Printer::db == `foomatic)
		{
		    if (model_db != "")
			UI::ChangeWidget (`id (`modelsel), `CurrentItem, model_db);
		}
		ret = nil;
	    }
	    if (`modelsel == ret)
	    {
		if (Printer::db == `foomatic)
		{
		    y2debug ("modelsel changed");
		    model_db = UI::QueryWidget (`id (`modelsel), `CurrentItem);
		    y2debug ("To %1", model_db);
		    info = $[];
		    string vc
			= Printer::foomatic[manuf_db,model_db, `vcomment]:"";
		    if (vc != "")
		    {
			y2error ("original vc: %1", vc);
			vc = replaceAll (vc, "\\n", "\n");
			y2error ("Replaced vc: %1", vc);
			vc = SCR::Read (.printerdb.translate, vc);
			y2error ("Translated vc: %1", vc);
		    }
		    string mc
			= Printer::foomatic[manuf_db,model_db, `mcomment]:"";
		    if (mc != "")
		    {
			y2error ("original mc: %1", mc);
                        mc = replaceAll (mc, "\\n", "\n");
                        y2error ("Replaced mc: %1", mc);
                        mc = SCR::Read (.printerdb.translate, mc);
                        y2error ("Translated mc: %1", mc);
		    }
		    integer type
			= Printer::foomatic[manuf_db,model_db, `support]:1;
		    if (vc != "")
			info["vendor"] = vc;
		    if (mc != "")
			info["printer"] = mc;
		    if (0 != type && info["printer"]:"" != "")
			DisplayPrinterPopup (info, manuf_db, model_db);
		    info_instead_next = ! (2 != type
//			&& size (filter (`k, `v,
//			    Printer::foomatic[manuf_db, model_db]:$[],
//			    ``(is (k, string)))) > 0
		    );
		}
		else
		{
		    list temp = UI::QueryWidget (`id (`modelsel), `CurrentItem);
		    model = select (temp, 0, "");
		    model_ieee = select (temp, 1, "");
		    info = SCR::Read (.printerdb.info, [ manuf, model ]);
		    if (0 != lookup (info, "type", 0) && "" != lookup (info, "printer", lookup (info, "config", "")))
		    {
		        // message box label
		        UI::LongTextPopup (_("Printer information"), `RichText (lookup (info, "printer", lookup (info, "config", ""))), 50, 10);
		    }
		    info_instead_next = ! ( 2 != lookup (info, "type", 0));
		}
	    }
	    ret = UI::UserInput ();
	    if (`abort == ret || `cancel == ret)
            {
                ret = reallyAbort () ? `abort : `cont;
                if (ret == `abort) break;
            }
		y2debug ("PDB: %1, MDB: %2", Printer::db, model_db);
	    if (ret == `next && (
		(Printer::db == `foomatic &&(model_db == "" || model_db == nil))
		|| (Printer::db != `foomatic && (model == "" || model == nil))
		))
	    {
		// popup
		UI::MessagePopup (_("Select a printer model."));
		UI::SetFocus (`id (`modelsel));
		ret = nil;
	    }
	    if (ret == `next && info_instead_next)
		ret = `infonext;
	    if (`useppd == ret)
            {
		boolean cont = true;
		if (Printer::spooler != "cups")
		    cont = UI::YesNoPopup (getPpdWarning ());
		ret = cont ? `useppd : `cont;
            }
	}

	if (ret == `next)
	{
	    boolean model_foomatic = false;
	    if (Printer::db == `foomatic && size (filter (`k, `v,
                  Printer::foomatic[manuf_db, model_db]:$[],
                  ``(is (k, string)))) > 0)
		// FIXME
	    {
		model_foomatic = true;
		manuf_ieee = Printer::foomatic[manuf_db, `label]:manuf_db;
		model_ieee
		    = Printer::foomatic[manuf_db, model_db, `label]:model_db;;
		manuf = manuf_ieee;
		model = model_ieee;
		Printer::printer = union (Printer::printer, $[
		    "vendor_db" : manuf_db,
		    "device_db" : model_db,
		]);
	    }
	    Printer::printer = union (Printer::printer, $[
		    "vendor_ieee" : manuf_ieee,
		    "device_ieee" : model_ieee,
		    "vendor" : manuf,
		    "device" : model,
		    "database" : Printer::db == `foomatic
			? "foomatic"
			: Printer::current_database,
		    "ownppd" : "",
		    "config" : "",
		    ]);
	    if (Printer::db == `foomatic && ! model_foomatic)
	    {
		y2milestone ("Using model not in Foomatic db, but current db is foomatic");
		list vendors = SCR::Read (.printerdb.vendors);
		foreach (`v, vendors, ``{
		    term t = select (v, 0, nil);
		    list l = select (t, 0, []);
		    string vid = select (l, 0, "");
		    string vieee = select (l, 1, "");
		    if ((vid != "" && vieee != "")
		      && (SCR::Read (.ppd.db.vendorname, vid) == manuf_db
		       || SCR::Read (.ppd.db.vendorname, vieee) == manuf_db))
		    {
			manuf = vid;
			manuf_ieee = vieee;
			y2milestone ("Found vendor: %1, %2", manuf, manuf_ieee);
		    }
		});
		list models = SCR::Read (add (.printerdb.models, manuf));
                foreach (`m, models, ``{
                    term t = select (m, 0, nil);
                    list l = select (t, 0, []);
                    string mid = select (l, 0, "");
                    string mieee = select (l, 1, "");
                    if ((mid != "" && mieee != "")
                      && (SCR::Read (.ppd.db.modelname, [manuf_db, mid]) == model_db
                       || SCR::Read (.ppd.db.modelname, [manuf_db, mieee]) == model_db))
                    {
                        model = mid;
                        model_ieee = mieee;
                        y2milestone ("Found model: %1, %2", model, model_ieee);
                    }
                });
		Printer::printer = union (Printer::printer, $[
		    "vendor_ieee" : manuf_ieee,
		    "device_ieee" : model_ieee,
		    "vendor" : manuf,
		    "device" : model,
		    "database" : nil,
		]);
		y2milestone ("Set printer %1", Printer::printer);
	    }
	}
	else if (ret == `back || ret == `abort)
	{
	    if (Printer::edit_seq)
	    {
	        if (tmp_printer_manuf_model != $[])
	        {
		    y2debug ("Setting printer back to %1", tmp_printer_manuf_model);
	            Printer::printer = eval (tmp_printer_manuf_model);
		    tmp_printer_manuf_model = $[];
	        }
	    }

	}
	return ret;
    }

    /**
     * Run dialog
     * @return symbol `back, `next, `abort
     */
    global define symbol runInstalledPrinterDialog () ``{
	y2milestone ("running installed printers dialog");
	list(map) printers = getConfiguredPrinters ();
	list items = [];

	integer counter = -1;
	list sel_items= maplist (`p, printers, ``{
	    counter = counter + 1;
	    if (p["ownppd"]:"" != "")
	    {
		string filename = lookup (p, "ownppd", "");
		if (filename != "" && Printer::isPpd (filename))
		{
		    map ppd = Printer::ppdInfo (filename);
		    filename = lookup (ppd, "manufacturer", "") + " : " + lookup (ppd, "model", "");
		}
		y2milestone ("Has own ppd, %1, printer is %2", filename, p);
		// selection box entry: <filename>, Parallel printer on /dev/lp0
		return `item (`id (counter), sformat (_("%1, %2"),
		    filename,
		    Printerlib::getUriNiceName (lookup (p, "uri", ""))));
	    }
		// eg. Epson Stylus Photo 810, Parallel printer on /dev/lp0
	    return `item (`id (counter), sformat (_("%1 %2, %3"),
		lookup (p, "vendor", ""), lookup (p, "device", ""),
		Printerlib::getUriNiceName (lookup (p, "uri", ""))));
	});

	term contents = `HBox (`HSpacing (2), `VBox (
	    `VSpacing (2),
		// selection box label
	    `SelectionBox (`id (`printers), `opt (`hstretch), _("&Select Printer:"), sel_items),
	    `VSpacing (2)
	), `HSpacing (2));

	// dialog box label
        Wizard::SetContentsButtons (_("Printers"), contents, getPrintersHelp (), BackButtonLabel (), NextButtonLabel ());
	UI::ChangeWidget (`id (`printers), `CurrentItem, 0);
	symbol ret = UI::UserInput ();
            if (ret == `cancel)
                ret = `abort;

	while (true)
	{
            if (`abort == ret || `cancel == ret)
            {
                ret = reallyAbort () ? `abort : `cont;
                if (ret == `abort) break;
            }
	    else if (`back == ret)
	    {
		break;
	    }
	    else if (`next == ret)
	    {
		integer ind = UI::QueryWidget (`id (`printers), `CurrentItem);
		Printer::printer = eval (printers[ind]:$[]);
		y2milestone ("Selected printer %1", Printer::printer);
		Printer::index = -1;
		Printer::printer["name"] = "";
		Printer::printer["options"] = $[];
		Printer::printer["config"] = "";
		break;
	    }
	    ret = UI::UserInput ();
            if (ret == `cancel)
                ret = `abort;
	}
	return ret;
    }

    /**
     * Dialog for choosing configuration.
     * @param ppd_only boolean true if shall not expand PPD files according
     *   to composite options
     * @return symbol for wizard sequencer
     */
    global define symbol runConfigDialog (boolean ppd_only) ``{
	y2milestone ("Running config dialog, ppd_only: %1", ppd_only);
	string manuf_db = lookup (Printer::printer, "vendor_db", "");
	string model_db = lookup (Printer::printer, "device_db", "");
	string manuf = lookup (Printer::printer, "vendor", "");
	string model = lookup (Printer::printer, "device", "");
	string config = lookup (Printer::printer, "config", "");
	map info = $[];
	list cfgs = [];
	map cfgsmap = $[];
	boolean foomatic = Printer::printer["database"]:"" == "foomatic"
	    || Printer::printer["ownppd"]:"" != "";
	y2milestone ("Configuring queue %1", Printer::printer);
	if (foomatic)
	{
	    loadFoomaticIfNeeded ();

	    map opts = Printer::printer["options"]:$[];
	    if (! ppd_only
		&& config != "" && Printer::printer["composite"]:"" != ""
		&& opts[Printer::printer["composite"]:""]:"" != "")
	    {
		config = sformat ("%1__\n__%2__\n__%3",
		    config,
		    Printer::printer["composite"]:"",
		    opts[Printer::printer["composite"]:""]:"");

	    }

	    list ppds = maplist (`k, `v, filter (`kk, `vv, Printer::foomatic[manuf_db, model_db]:$[], ``(is (kk, string))), ``(k));

	    if (Printer::printer["ownppd"]:"" != "")
	    {
		y2milestone ("proposing from custom PPD file");
		ppds = [`ownppd];
	    }
	    else
	    {
		ppds = sortPpds (manuf_db, model_db, ppds);
	    }

	    cfgs = [];
	    foreach (`k, ppds, ``{
		string ppdfile_orig = "";

		if (k == `ownppd && Printer::printer["ownppd"]:"" != "")
		    ppdfile_orig = Printer::printer["ownppd"]:"";
		else if (is (k, string))
		{
		    y2debug ("V: %1, M: %2, K: %3", manuf_db, model_db, k);
		    ppdfile_orig = Printer::foomatic[manuf_db, model_db, k, 0]:"";
		}

		if (ppdfile_orig == "")
		    return;

		if (k == `ownppd)
		{
		    cfgs = add (cfgs, `item (`id (""),
                            // selection box item
                            _("Default Configuration")));
		    cfgsmap[""] = _("Default Configuration");
		}
		else if (is (k, symbol))
		{
		    return;
		}
		else
		{			// done inside agent
		    string label = k;//sformat ("%1 (%2)", k, ppdfile_orig);
		    cfgs = add (cfgs, `item (`id (k), label));
		    cfgsmap[k] = label;
		}

		if (ppd_only)
		{
		    return;
		}
		if (ppdfile_orig == "")
		{
		    return;
		}
		string ppdfile = SCR::Read (.ppd.file.open,
		    [ppdfile_orig, SCR::Read (.target.tmpdir)]);
		map options = SCR::Read (.ppd.file.options, [ppdfile, ""]);
		list opts = options["data"]:$[];
// FIXME multiple composite options checking ????
		integer ind = -1;
		boolean found = true;
		find (`i, opts, ``{
		    ind = ind + 1;
		    found = i["name"]:"" == "PrintoutMode";
		    return found;
		});
		map composite = $[];
		if (found)
		    composite = opts[ind]:$[];
		if (composite == $[])
		{
		    return;
		}

		string ppd_label_part = "";
		if (k != `ownppd)
		    ppd_label_part = k;
		else
		{
		    // selection box item
		    ppd_label_part = _("Custom PPD File");
		}
		if (regexpmatch (k, ".*, .*"))
		{
		    ppd_label_part = select (regexptokenize (k, ".*, (.*)"), 0, k);
		}

                foreach (`v, composite["valorder"]:[], ``{
                    string optname = composite["name"]:"";
		    string complabel = composite["values", v]:v;
		    cfgs = add (cfgs, `item (
			`id (sformat ("%1__\n__%2__\n__%3", k, optname, v)),
			sformat ("    %1"/* (%2)"*/, complabel/*, ppd_label_part*/)));
		    cfgsmap[sformat ("%1__\n__%2__\n__%3", k, optname, v)]
			= sformat ("%1"/* (%2)"*/, complabel/*, ppd_label_part*/);
                });

	    });
	}
	else
	    cfgs = SCR::Read (add (.printerdb.configsrt,model), Printer::spooler);
	term contents = `HBox (
	    `HSpacing (1),
	    `VBox (
//		    `RichText (select (cfgs, 0, "")),
		    `VSpacing (1),
		    // selection box label
		    `SelectionBox (`id (`selection), `opt (`hstretch), _("&Select configuration:"), foomatic ? cfgs : select (cfgs, 1, [])),
		    `VSpacing (1),
		    ppd_only ? `VSpacing (0) : `HBox (
			`HStretch (),
			// pushbutton
			`HWeight (1, `PushButton (`id (`test), `opt (`key_F6), _("&Test printing"))),
			// pushbutton
			-1 == Printer::index ? `HWeight (1, `PushButton (`id (`advanced), `opt (`key_F7), _("Ad&vanced settings"))) : `HSpacing (0),
			`HStretch ()
		    ),
		    `VSpacing (ppd_only ? 0 : 1)
		),
	    `HSpacing (1)
	);
	// dialog box label
	string caption = ppd_only ? _("PPD Files")
		// dialog box label
	    : _("Configurations");
	Wizard::SetContentsButtons (caption, contents, ppd_only ? getPpdSelectHelp () : getConfigHelp (), BackButtonLabel (), OKButtonLabel ());

	if (config != "" && config != nil)
	{
	    UI::ChangeWidget (`id (`selection), `CurrentItem, config);
	}

	if (! foomatic)
	{
	boolean found = false;
	if (config == "")
	{
	    if (Printer::spooler == "cups")
	    {
		foreach (`e, select (cfgs, 1, []), ``{
		    string conf = select (select (e, 0, `id("0")), 0, "");
		    if ( !found && lookup (SCR::Read (.printerdb.configinfo, conf), "cups", false))
		    {
			found = true;
			UI::ChangeWidget (`id (`selection), `CurrentItem, conf);
		    }
		});
	    }
	    else if (Printer::spooler == "lprng")
	    {
		foreach (`e, select (cfgs, 1, []), ``{
		    string conf = select (select (e, 0, `id("0")), 0, "");
		    if ( !found && lookup (SCR::Read (.printerdb.configinfo, conf), "cups", false))
		    {
			found = true;
			UI::ChangeWidget (`id (`selection), `CurrentItem, conf);
		    }
		});
	    }
	    else
	    {
                foreach (`e, select (cfgs, 1, []), ``{
                    string conf = select (select (e, 0, `id("0")), 0, "");
                    if ( !found && lookup (SCR::Read (.printerdb.configinfo, conf), "cups", false) && lookup (SCR::Read (.printerdb.configinfo, conf), "lprng", false))
                    {
                        found = true;
                        UI::ChangeWidget (`id (`selection), `CurrentItem, conf);
                    }
                });

	    }
	}

	if (! found )
	{
	    if (config != "" && contains (select (cfgs, 1, []), config))
		    UI::ChangeWidget (`id (`selection), `CurrentItem, config);
	    else
	    {
	        term e = select (select (cfgs, 1, []), 0, nil);
	        if (e != nil)
	        {
		    string conf = select (select (e, 0, `id("0")), 0, "");
		    UI::ChangeWidget (`id (`selection), `CurrentItem, conf);
	        }
	    }
	}
	}
	if (foomatic && (config == "" || config == nil))
	{
            {
		term item = cfgs[0]:nil;
		if (nil != item)
		{
		    config = select (select (item, 0, nil), 0, "");
		}
            }
	    if (config != "" && config != nil)
	    {
		UI::ChangeWidget (`id (`selection), `CurrentItem, config);
	    }
	}

	any ret = `config;
	while (`abort != ret && ret != `back && ret != `next && `advanced != ret)
	{
	    if (is (ret, string))
	    {
		config = ret;
		ret = `next_x;
		if (foomatic)
		    info = SCR::Read (.printerdb.configinfo, config);
		if (!lookup (info, "supported", true) && "" != lookup (info, "comment", ""))
		    ret = `info;
		else
		    break;
	    }
	    if (`info == ret)
	    {
		// popup
		string i = lookup (info, "comment", _("<P>No information available.</P>"));
		// popup header
		UI::LongTextPopup (_("Configuration information"), `RichText (i), 50, 18);
	    }
	    if (`abort == ret)
	    {
		ret = reallyAbort () ? `abort : nil;
	    }
	    if (`test == ret)
	    {
		map tmpPrinter = Printer::printer;
		config = UI::QueryWidget (`id (`selection), `CurrentItem);
        // Why this? The new selected configuration cannot be tested
        // before saving:
		if (lookup (tmpPrinter, "config", "") != config)
		    tmpPrinter = add (tmpPrinter, "options", $[]);
		if (foomatic && regexpmatch (config, ".*__\n__.*__\n__.*"))
		{
	            list tokenized
	                = regexptokenize (config, "(.*)__\n__(.*)__\n__(.*)");
	            config = tokenized[0]:"";
	            string key = tokenized[1]:"";
	            string val = tokenized[2]:"";
		    tmpPrinter["composite"] = key;
	            map opts = Printer::printer["options"]:$[];
	            opts[key] = val;
	            tmpPrinter["options"] = opts;
		}
		else
		{
		    tmpPrinter["composite"] = "";
		}
		tmpPrinter = adjustPaperSize (tmpPrinter);
		tmpPrinter = add (tmpPrinter, "config", config);

		testPrinter (tmpPrinter, -1);
	    }
	    ret = UI::UserInput ();
            if (ret == `cancel)
                ret = `abort;
	}
	if (ret == `advanced)
	{
	    config = UI::QueryWidget (`id (`selection), `CurrentItem);
	    if (lookup (Printer::printer, "config", "") != config)
		Printer::printer = add (Printer::printer, "options", $[]);
            if (foomatic && regexpmatch (config, ".*__\n__.*__\n__.*"))
            {
		list tokenized
		    = regexptokenize (config, "(.*)__\n__(.*)__\n__(.*)");
		config = tokenized[0]:"";
		string key = tokenized[1]:"";
		Printer::printer["composite"] = key;
		string val = tokenized[2]:"";
		map opts = Printer::printer["options"]:$[];
		opts[key] = val;
		Printer::printer["options"] = opts;
		Printer::printer = adjustPaperSize (Printer::printer);
//		Printer::printer["composite_label"] = composite["values", val]:"";
            }
	    else
	    {
		Printer::printer["composite"] = "";
	    }

	    Printer::printer = add (Printer::printer, "config", config);
	    return ret;
	}
	if (ret == `next || ret == `next_x)
	{
	    if (`next == ret)
		config = UI::QueryWidget (`id (`selection), `CurrentItem);
	    if (lookup (Printer::printer, "config", "") != config)
		Printer::printer = add (Printer::printer, "options", $[]);
	    string config_id = config;
            if (foomatic && regexpmatch (config, ".*__\n__.*__\n__.*"))
            {
	        list tokenized
		    = regexptokenize (config, "(.*)__\n__(.*)__\n__(.*)");
	        config = tokenized[0]:"";
	        string key = tokenized[1]:"";
		Printer::printer["composite"] = key;
	        string val = tokenized[2]:"";
	        map opts = Printer::printer["options"]:$[];
	        opts[key] = val;
	        Printer::printer["options"] = opts;
		Printer::printer = adjustPaperSize (Printer::printer);
            }
            else
            {
                Printer::printer["composite"] = "";
            }
	    // add comment
	    Printer::printer["comment"] = cfgsmap[config_id]:config_id;
/*	    foreach (`i, foomatic ? cfgs : select (cfgs, 1, []), ``{
		if (select (select (i, 0, []), 0, nil) == config_id
		    && select (i, 1, nil) != nil)
		{
		    Printer::printer["comment"] = select (i, 1, nil);
		}
	    });*/

	    Printer::printer = add (Printer::printer, "config", config);
	    // FIXME: check if it is not necessary to clean options.
	    ret = `next;
	}
	return ret;
    }

    /**
     * Configuration of printer names.
     * @param detected boolean trueue if dialog is run for detected printer
     * @return symbol for wizard seq.
     */
    global define symbol runNameDialog (boolean detected) ``{
	y2milestone ("Running name dialog for %1", Printer::printer);
	list forbidden_names = Printer::getForbiddenNames ();
	string name = lookup (Printer::printer, "name", "");
	string old_name = name;
	if (lookup (Printer::printer, "info", nil) == nil)
	    Printer::printer = add (Printer::printer, "info", "");
	string info = lookup (Printer::printer, "info", "");
	if (lookup (Printer::printer, "location", nil) == nil)
	    Printer::printer = add (Printer::printer, "location", "");
	string loc = lookup (Printer::printer, "location", "");
	boolean is_prefilter_editable = ("prefilter" == Printer::getUriType () && -1 == Printer::index && lookup (Printer::printer, "create_remote_queue", false));
	string remote = "";
	if (lookup (Printer::printer, "raw", nil) == nil)
	    Printer::printer = add (Printer::printer, "raw", false);
	if (lookup (Printer::printer, "ff", nil) == nil)
	    Printer::printer = add (Printer::printer, "ff", false);
	boolean raw = lookup (Printer::printer, "raw", false);
	boolean ff = lookup (Printer::printer, "ff", false);
	boolean is_class = Printerlib::getUriType (lookup (Printer::printer, "uri", "")) == "class" ? true : false;

	if (-1 != Printer::index)
	{
	    // remove me from forbidden names
	    forbidden_names = filter (`i, forbidden_names, ``{ return !(i == name); });
	}
	if ("" == name || nil == name)
	{
	    // generate default name
	    name = is_class ? generateClassName (forbidden_names) : generateQueueName ("lp", forbidden_names);
	}
	if (is_prefilter_editable) {
	    remote = Printerlib::getUriRemoteQueue (lookup (Printer::printer, "uri", ""));
	    if ("" == remote || nil == remote)
	    {
		remote = "remote";
	    }
	    remote = generateQueueName (remote, add (forbidden_names, name));
	}

	if (is_class)
	{
	    if (lookup (Printer::printer, "options", $[]) == $[])
		Printer::printer = add (Printer::printer, "options", $["job-sheets": "none,none"]);
	}

	term insert =
	    "lpd" == Printer::getUriType () /*|| "cups" == Printer::spooler*/ ? `VSpacing (0) :
		// check box
		`CheckBox (`id (`raw), `opt (`hstretch), _("Ra&w queue"), lookup (Printer::printer, "raw", raw));

	term insert2 =
	     "cups" == Printer::spooler ? `VSpacing (0) :
		// check box
		`CheckBox (`id (`ff), `opt (`hstretch), _("Print &formfeed between jobs"), lookup (Printer::printer, "ff", ff));

	term insert3 = detected && ! Printer::printer["modelset_required"]:false
	    ? `VBox (
		    `HBox (
			`HStretch (),
			// pushbutton
			`PushButton (`id (`model), _("&Specify Model Manually"))
		    ),
		    `VSpacing (2)
		)
	    : `VSpacing (0);

	term contents = `VBox (
		// text entry
		`TextEntry (`id (`name), `opt (`hstretch), _("Name for &printing:"), name),
		`VSpacing (1),
		"lprng" == Printer::spooler ? `VSpacing (0) :
		// text entry
		    `TextEntry (`id (`info), `opt (`hstretch), is_class ? _("&Description of class")
		// text entry
			 : _("&Description of Printer"), info),
		"lprng" == Printer::spooler ? `VSpacing (0) :
		// text entry
		    `TextEntry (`id (`loc), `opt (`hstretch), is_class ? _("&Location of class")
		// text entry
			 : _("&Location of Printer"), loc),
		`VSpacing ("cups" == Printer::spooler ? 0 : 1),
		insert,
		insert2,
		`VSpacing (1)
		);
	if (is_prefilter_editable)
	{
	    contents = add (add (contents,
		    `TextEntry (`id (`prefilter), `opt (`hstretch),
			// textentry
			_("Name for &TCP/IP forwarding queue"), remote)),
		    `VSpacing (1));
	}
	contents = `HBox (`HSpacing (4),
		// frame
		`VBox (
			// frame label
		    `Frame (_("Queue name and spooler settings"), contents),
		    `VSpacing (2),
		    insert3
		),
		`HSpacing (4));

	// dialog box label
	Wizard::SetContentsButtons (
		// dialog caption
		is_class ? _("Class name") : _("Queue name"),
		contents,
		getSpoolInfoHelp (is_prefilter_editable, "lpd" != Printer::getUriType ()),
		BackButtonLabel (),
		NextButtonLabel ()
		);
	UI::ChangeWidget (`id (`name), `ValidChars, "_0123456789abcdefghijklmnopqrstuvwxyz-");
	if (is_prefilter_editable)
	{
	    UI::ChangeWidget (`id (`prefilter), `ValidChars, "_0123456789abcdefghijklmnopqrstuvwxyz-");
	}
/*	if (-1 != Printer::index && lookup (Printer::printer, "type", "yast2") == "yast2" && Printer::spooler != "cups")
	{
	    UI::ChangeWidget (`id (`raw), `Enabled, false);
	}*/

	any ret = nil;
	string fwd = "";
	repeat {
	    ret = UI::UserInput ();
            if (ret == `cancel)
                ret = `abort;
	    if (ret == `next)
	    {
		raw = ("lpd" == Printer::getUriType () ? true : UI::QueryWidget (`id (`raw), `Value));
		if (Printer::spooler != "cups")
		{
//		    raw = ("lpd" == Printer::getUriType () ? true : UI::QueryWidget (`id (`raw), `Value));
		    ff = UI::QueryWidget (`id (`ff), `Value);
		}
		else
		{
//		    raw = false;
		    ff = false;
		}
		name = tolower (UI::QueryWidget (`id (`name), `Value));
		if (Printer::spooler != "lprng")
		{
		    info = UI::QueryWidget (`id (`info), `Value);
		    loc = UI::QueryWidget (`id (`loc), `Value);
		}
		if (checkName (forbidden_names, name) && checkLocalQueueName (name))
		{
		    if (is_prefilter_editable)
		    {
			fwd = tolower (UI::QueryWidget (`id (`prefilter), `Value));
			if (!checkName (add (forbidden_names, name), fwd) && checkLocalQueueName (fwd))
			{
			    continue;
			}
		    }
		    break;
		}
		ret = nil;
	    }
	    if (`abort == ret || `cancel == ret)
	    {
		ret = reallyAbort () ? `abort : nil;
	    }
	} until (`abort == ret || ret == `back || ret == `next || `model == ret);

	if (ret == `next || ret == `model)
	{
	    if (raw == nil)
		raw = false;
	    if (ff == nil)
		ff = false;
	    Printer::printer = add (Printer::printer, "name", name);
	    if (!is_class)
	    {
		Printer::printer = add (Printer::printer, "raw", raw);
		Printer::printer = add (Printer::printer, "ff", ff);
	    }
	    Printer::printer = add (Printer::printer, "info", info);
	    Printer::printer = add (Printer::printer, "location", loc);
	    if (is_prefilter_editable)
	    {
		Printer::printer = add (Printer::printer, "uri", "prefilter://" + fwd);
	    }
	    if (raw)
	    {
		ret = `raw;
	    }
	    if (is_class)
		ret = `class;

	    if (old_name != "" && old_name != name)
	    {
		Printer::addToDeleted (old_name, is_class ? "class" : "printer");
//		Printer::deleted = add (Printer::deleted, $["name": old_name, "type": is_class ? "class" : "printer",
//			"states" : $["cups-state" : "changed", "lprng-state" : "changed"] ]);
	    }

//	    if (ret == `next && Printer::printer["modelset_required"]:false)
//		ret = `model;
	}
	y2milestone ("Returning %1", ret);
	return ret;
    }
    /**
     * Choose one printer from list of printers
     * @return string selected printer
     */
    global define string choosePrinterOrClass () ``{
	list onlyprinters = filter (`e, Printer::printcap, ``(lookup (e, "uri", "class") != "class"));
        list printers =
            flatten ([
		toset (
		    maplist (`i, filter (
			`e, Printer::printcap, ``(
			    lookup (e, "uri", "class") != "class"
			)
		    ), ``{
			return lookup (i, "name", "");
                    })
		)
            ]);

        UI::OpenDialog (
                `HBox (
                    `HSpacing (0.7),
                    `VSpacing (16),
                    `VBox (
                        `VSpacing (0.5),
                        `HSpacing (16),
                        // selection box label
                        `SelectionBox ( `id (`printers), _("Choose &printer:"), maplist (`i, printers, ``{return `item (`id(i),i);})),
                        `HBox (
                            `PushButton (`id (`ok), `opt (`key_F10), OKButtonLabel ()),
                            `PushButton (`id (`cancel), `opt (`key_F9), CancelButtonLabel ())
                            ),
                        `VSpacing (0.3)
                        ),
                    `HSpacing (0.7)
                    )
                );

        string member = "";
        symbol ret = nil;
        while (true)
            {
                ret = UI::UserInput ();
                if (`ok == ret)
                    {
                        member = UI::QueryWidget (`id (`printers), `CurrentItem);
                        break;
                    }
                else if (`cancel == ret)
                    {
                        break;
                    }
            }
        UI::CloseDialog ();
        return member;
    }
    /**
     * Members of class
     * @return symbol `next or `back
     */
    global define symbol runMembersDialog () ``{
        list printers = toset (lookup (Printer::printer, "printers", []));
        term contents =
            `VBox (
                `ReplacePoint (
                    `id (`replace_members),
                    `SelectionBox(
                        `id (`printers),
                        // selection box label
                        _("&Following printers and classes are members of this class"),
                        maplist (`i, printers, ``{
                            return `item (`id (i), i);
                        })
                        )
                    ),
                `HBox(
                    `PushButton(`id(`add), `opt (`key_F3), AddButtonLabel ()),
                    `PushButton(`id(`delete), `opt (`key_F5), DeleteButtonLabel ())
                    )
                );
        // dialog label
        Wizard::SetContentsButtons (_("Members of class"), contents, getMembersHelp (), BackButtonLabel (), NextButtonLabel ());

        symbol ret = nil;
        while (true)
        {
            ret = UI::UserInput ();
            if (ret == `cancel)
                ret = `abort;
            if (`delete == ret)
                {
                    string member_to_delete = UI::QueryWidget (`id (`printers), `CurrentItem);
                    if (nil != member_to_delete)
                        {
                            printers = DeleteItem (printers, member_to_delete);
                            UI::ReplaceWidget (
                                `id (`replace_members),
                                `SelectionBox(
                                    `id (`printers),
                                    // selection box label
                                    _("&Following printers and classes are members of this class"),
                                    maplist (`i, printers, ``{
                                        return `item (`id (i), i);
                                    })));
                        }
                    else
                        {
                            // message box
                            UI::MessagePopup (_("Select printer to delete."));
                        }
                }
            else if (`add == ret)
                {
                    string member_to_add = choosePrinterOrClass ();
		    if (member_to_add != "")
		    {
			printers = toset (add (printers, member_to_add));
			UI::ReplaceWidget (
			    `id (`replace_members),
			    `SelectionBox(
				`id (`printers),
				// selection box label
				_("&Following printers and classes are members of this class"),
				maplist (`i, printers, ``{
				    return `item (`id (i), i);
				})));
		    }
                }
            else if (`next == ret)
                {
                    if (0 != size (printers))
                        {
                            Printer::printer = add (Printer::printer, "printers", printers);
                            break;
                        }
                    else
                        {
                            // message box
                            UI::MessagePopup (_("Class must contain at least one printer."));
                        }
                }
            else if (`back == ret)
                {
                    break;
                }
            else if (`abort == ret)
                {
                    if (reallyAbort ())
                        {
                            break;
                        }
                }
        }
        return ret;
    }




    /**
     * Configuration of the filter. It is quite complicated because there can
     * be depended options so change of value (lower list) can change content
     * of options (upper list).
     *
     * Request from jsmeix@suse.de: If user edited upp file manually, yast2 should not overwrite it.
     *
     * Little work is done here: value of "newer_gs_upp" is used to determine if upp file was changed after
     * it was saved by yast2. If yes, situation is explained to user and user must choose if he wants to edit
     * the settings or keep them untouched.
     *
     * @return symbol for ws
     */
    global define symbol runOptionsDialog () ``{
	boolean foomatic = Printer::printer["database"]:"" == "foomatic";
        if (foomatic)
            loadFoomaticIfNeeded ();
	foomatic = foomatic || Printer::printer["ownppd"]:"" != "";
	string filename = "";
	if (foomatic)
	{
	    string orig_name = Printer::foomatic[Printer::printer["vendor_db"]:"", Printer::printer["device_db"]:"", Printer::printer["config"]:"", 0]:"";
	    if (Printer::printer["ownppd"]:"" != "")
		orig_name = Printer::printer["ownppd"]:"";
	    if (orig_name == "")
	    {
		// error report
		Report::Error (_("Unknown PPD file. Select printer model."));
		return `back;
	    }
	    filename = SCR::Read (.ppd.file.open,
		[orig_name, SCR::Read (.target.tmpdir)]);
	    if (filename == "")
	    {
		Report::Error (sformat (
			// error report
		    _("Error occurred while opening PPD file
%1
"),
		    orig_name));
		return `back;
	    }
	}

	Printer::printer = add (Printer::printer, "newer_gs_upp", false);
	map saved = lookup (Printer::printer, "saved", $[]);
	saved = add (saved, "upp_fname", "");
	saved = add (saved, "upp", "");
	Printer::printer = add (Printer::printer, "saved", saved);
	string config = lookup (Printer::printer, "config", "");
	any vals = nil;

    term test_button = (!Printer::isNetAvailable () && Printer::isNetwork ())
	// pushbutton
    ? `VSpacing (1) : `HWeight (1, `PushButton (`id (`test), `opt (`key_F6), _("&Test")));

	// dialog caption
	Wizard::SetContentsButtons (_("Configuration options"), `VBox (
		`VWeight (1, `ReplacePoint (`id (`upper), `VSpacing (1))),
		`VWeight (1, `ReplacePoint (`id (`lower), `VSpacing (1))),
		`HBox (`HStretch (), test_button, `HStretch ())
		), getFilterHelp (), BackButtonLabel (), NextButtonLabel ());

	map selected = lookup (Printer::printer, "options", $[]);

	list opts = [];
	list optlist = []; // used only for foomatic;
        if (foomatic)
        {
            map options = SCR::Read (.ppd.file.options, [filename, ""]);
            optlist = options["data"]:[];
	    y2error ("First option: %1", optlist[0]:$[]);
	    optlist = filter (`o, optlist, ``(
		o["name"]:"" != "PageRegion"
		&& o["name"]:"" != "ImageableArea"
		&& o["mame"]:"" != "PaperDimension"));
	    optlist = add (optlist, $[
		"current" : "3",
			// list item
		"gui" : _("Orientation"),
		"name" : "orientation-requested",
		"type" : "PickOne",
		"valorder" : ["3", "4", "5", "6"],
				// list item
		"values" : $["3" : _("Portrait"),
				// list item
			"4" : _("Landscape"),
				// list item
			"5" : _("Reverse"),
				// list item
			"6" : _("Reverse portrait"),
		],
	    ]);
	    optlist = add (optlist, $[
		"current" : "1",
			// list item
		"gui" : _("Pages per sheet"),
		"name" : "number-up",
		"type" : "pick_one",
		"valorder" : ["1", "2", "4"],
		"values" : $[
			"1" : "1",
			"2" : "2",
			"4" : "4",
		],
	    ]);
            opts = maplist (`i, optlist, ``(
                `item (`id (i["name"]:""), i["gui"]:"")
            ));
	}
	else
	    opts = SCR::Read (add (.printerdb.options, config), selected);

	symbol ret = `opts;
	if (! foomatic && ! havePaperSize (selected))
	{
	    selected = solvePaperSize (selected, opts);
	    opts = SCR::Read (add (.printerdb.options, config), selected);
	}
	// selection box label
	UI::ReplaceWidget (`id (`upper), `SelectionBox (`id (`opts), `opt (`notify), _("&Options"), opts));

	if (foomatic)
	{
	    UI::ChangeWidget (`id (`opts), `CurrentItem,
		lookup (select (lookup (SCR::Read (.ppd.file.options,
			 [filename, ""]),
		    "data", []), 0, $[]), "name", ""));
	}

	while (`next != ret && `back != ret && `abort != ret)
	{
	    if (`test == ret)
	    {
		Printer::printer = add (Printer::printer, "options", selected);

		testPrinter (Printer::printer, -1);
	    }
	    if (`opts == ret)
	    {
		if (foomatic)
		{
		    string option = UI::QueryWidget (`id (`opts), `CurrentItem);
		    string ppd_default = "";
		    foreach (`i, optlist, ``{
			if (i["name"]:"" == option)
			{
			    vals = maplist (`k, i["valorder"]:$[], ``(
				`item (`id (k), i["values", k]:k)
			    ));
			    ppd_default = i["current"]:"";
			    if (ppd_default == "")
				ppd_default = i["valorder", 0]:"";
			}
		    });
		    UI::ReplaceWidget (`id (`lower), getValuesUI (vals));
		    // FIXME type of option
		    {
			string current = selected[option]:"";
			if (current == "")
			    current = ppd_default;
			UI::ChangeWidget (`id (`vals), `CurrentItem, current);
		    }
		}
		else
		{
		    list idx = UI::QueryWidget (`id (`opts), `CurrentItem);
		    string sel = select (idx, 1, "");
		    vals = SCR::Read (add (.printerdb.values, sel), lookup (selected, select (idx, 0, ""), nil));
		    UI::ReplaceWidget (`id (`lower), getValuesUI (vals));
	            string comment = SCR::Read (add (.printerdb.option_comment, sel));
	            if (nil != comment && "" != comment)
	                UI::LongTextPopup ("", `RichText (comment), 50, 11);
		}
	    }
	    else if (`vals == ret || `vals2 == ret)
	    {
		if (foomatic)
		{
		    string o = UI::QueryWidget (`id (`opts), `CurrentItem);
		    string v = UI::QueryWidget (`id (`vals), `CurrentItem);
		    selected[o] = v;
		}
		else
		{
		    list idx = UI::QueryWidget (`id (`opts), `CurrentItem);
		    string co = select (idx, 0, "");
		    selected = add (selected, co, grabValueUI (vals));
		    opts = SCR::Read (add (add (.printerdb.options, config), co), selected);
		    // selection box label
		    UI::ReplaceWidget (`id (`upper), `SelectionBox (`id (`opts), `opt (`notify), _("&Options"), opts));
		}
	    }
	    ret = UI::UserInput ();
            if (ret == `cancel)
                ret = `abort;
	}
	if (`next == ret)
	{
	    string ps = selected["PageSize"]:nil;
	    if (ps != nil)
	    {
		selected["PageRegion"] = ps;
		selected["ImageableArea"] = ps;
		selected["PaperDimension"] = ps;
	    }
	    map saved = lookup (Printer::printer, "saved", $[]);
	    saved = add (saved, "upp_fname", "");
	    saved = add (saved, "upp", "");
	    Printer::printer = add (Printer::printer, "options", selected);
	    Printer::printer = add (Printer::printer, "saved", saved);
	    if (Printer::spooler == "cups")
		ret = `next_cups;
	}
	return ret;
    }

    /**
     * Choose one of remote printer (fwd queues).
     * @return symbol for ws
     */
    global define symbol runConnectionDialog () ``{
	y2milestone ("Connection dialog, confmode: %1", Printer::confmode);
	boolean show_all = -1 == Printer::index || lookup (Printer::printer, "type", "yast2") != "yast2";
	string uritype = Printer::getUriType ();
	if ("" == uritype)
	    uritype = "parallel";
	term contents =
	    `VBox (
		`HBox (
		    `HSpacing (5),
		    `Frame (
			// label of the frame
			_("Select the printer type:"),
			`VBox (
			    `RadioButtonGroup (`id (`connections), connectionsRadioButtons (uritype, show_all, Printer::confmode == `adm)),
			    `VSpacing (1))
			),
		    `HSpacing (5)),
		`VSpacing (2));

	boolean new_queue = false;
	if (size (getConfiguredPrinters ()) != 0 && Printer::confmode == `adm)
	    new_queue = true;
	//label of the dialog
	Wizard::SetContentsButtons (_("Connection for printer:"), contents, show_all ? getConnectionHelp (new_queue) : getLocalConnectionHelp (), BackButtonLabel (), NextButtonLabel ());
	Wizard::RestoreBackButton ();
	symbol ret = nil;
	symbol type = nil;

	repeat {
	    ret = UI::UserInput ();
            if (ret == `cancel)
                ret = `abort;
	    type = UI::QueryWidget (`id (`connections), `CurrentButton);
	    if (`abort == ret || `cancel == ret)
		{
		    ret = reallyAbort () ? `abort : nil;
		}
	} until (`abort == ret || ret == `back || (type != nil && ret == `next));

	if (`next == ret)
	{
	    ret = type;
	    Printer::printer = add (Printer::printer, "type", "yast2");
	    if (type == `class)
		Printer::printer = add (Printer::printer, "uri", "class");
	}
	return ret;
    }

    /**
     * Choose one of remote printer (fwd queues).
     * @return symbol for ws
     */
    global define symbol runAdvancedConnectionDialog () ``{
        string uritype = Printer::getUriType ();
        if ("" == uritype)
            uritype = "parallel";
        term contents =
            `VBox (
                `HBox (
                    `HSpacing (5),
                    `Frame (
                        // label of the frame
                        _("Select the printer type:"),
                        `VBox (
                            `RadioButtonGroup (`id (`connections), advancedConnectionsRadioButtons (uritype, Arch::s390 && Printer::confmode == `adm)),
                            `VSpacing (1))
                        ),
                    `HSpacing (5)),
                `VSpacing (2));

	symbol helptype = `all;
	if (Printer::spooler == "cups")
	    helptype = `cups;
	if (Printer::spooler == "lprng")
	    helptype = `lprng;
        boolean new_queue = false;
        if (size (getConfiguredPrinters ()) != 0 && Printer::confmode == `adm && Arch::s390)
            new_queue = true;

	//label of the dialog
        Wizard::SetContentsButtons (_("Connection for printer:"), contents, getAdvancedConnectionHelp (helptype, new_queue), BackButtonLabel (), NextButtonLabel ());
        symbol ret = nil;
        symbol type = nil;

        repeat {
            ret = UI::UserInput ();
            if (ret == `cancel)
                ret = `abort;
            type = UI::QueryWidget (`id (`connections), `CurrentButton);
            if (`abort == ret || `cancel == ret)
                {
                    ret = reallyAbort () ? `abort : nil;
                }
        } until (`abort == ret || ret == `back || (type != nil && ret == `next));

        if (`next == ret)
        {
            ret = type;
            Printer::printer = add (Printer::printer, "type", "yast2");
            if (type == `class)
                Printer::printer = add (Printer::printer, "uri", "class");
        }
        return ret;
    }


    /**
     * Create contents for edit dialog. Calls Setwizardcontents at the end.
     */
    global define void createEditDialog () ``{
	string help_text = "";
	// determine printer type ...
	//
	string uri = lookup (Printer::printer, "uri", "");
	string uri_type = Printerlib::getUriType (uri);
	boolean conf_type = lookup (Printer::printer, "type", "yast2") == "yast2";
	boolean know = conf_type || lookup (Printer::printer, "type", "yast2") == "non-yast-now";
	boolean raw = lookup (Printer::printer, "raw", false);
	boolean foomatic = Printer::printer["database"]:"" == "foomatic";
	string model = lookup (Printer::printer, "device", "");

	//
	// texts
	//
	string model_text = foomatic
	    ? sformat ("%1 %2", Printer::printer["vendor"]:"",
		Printer::printer["device"]:"")
	    : SCR::Read (.printerdb.printername,
		[ lookup (Printer::printer, "vendor", ""), lookup (Printer::printer, "device", ""),
		  lookup (Printer::printer, "vendor_ieee", nil), lookup (Printer::printer, "device_ieee", nil) ]);
	string config_text = "";
	if (foomatic)
	{
	    config_text = Printer::printer["config"]:"";
	    if (Printer::printer["composite"]:"" != "")
	    {
		string composite = Printer::printer["composite"]:"";
		if (composite != "")
		{
		    config_text = Printer::printer["comment"]:"";
		}
	    }
	}
	else
	{
	    list cfgs = SCR::Read (add (.printerdb.configsrt,model),
		Printer::spooler);
	    cfgs = select (cfgs, 1, []);
	    string key = Printer::printer["config"]:"";
	    foreach (`c, cfgs, ``{
		string id = select (select (c, 0, []), 0, nil);
		if (id == key)
		    config_text = select (c, 1, "");
	    });
	}
	string name_text = lookup (Printer::printer, "name", "");
	string connection_text = "";
	list items = [];
	string rich = "";

	// table entry
	items = add (items, `item (`id (`name), _("Name and basic settings"), lookup (Printer::printer, "name", "")));
	if ("class" != uri_type)
	{
            if (uri_type != "lpd" && ! lookup (Printer::printer, "raw", false))
            {
                if (lookup (Printer::printer, "ownppd", "") != "")
                {
                    string filename = lookup (Printer::printer, "ownppd", "");
                    if (filename != "" && Printer::isPpd (filename))
                    {
                        map ppd = Printer::ppdInfo (filename);
                        filename = lookup (ppd, "manufacturer", "") + " : " + lookup (ppd, "model", "");
                    }
                    else
                    {
                        filename = "";
                    }
			// table entry
                    items = add (items, `item (`id (`ppd), _("PPD file"), know ? filename : _("unknown")));

                }
                else
                {
			// table entry
                    items = add (items, `item (`id (`model), _("Printer model"), know ? model_text : _("unknown")));
			// table entry
                    items = add (items, `item (`id (`config), _("Printer configuration"), config_text));
                }
            }
		// table entry
	    items = add (items, `item (`id (`connection), _("Connection"), know ? Printerlib::getUriNiceName (uri) : _("unknown")));
	}
	if ("class" == uri_type)
	{
	// table entry
	    items = add (items, `item (`id (`class_members), _("Class members"), ""));
	// table entry
	    items = add (items, `item (`id (`perm), _("Restrictions settings"), ""));
	// table entry
	    items = add (items, `item (`id (`state), _("State and banners settings"), ""));
	}
	else
	{
	    if (uri_type != "lpd" && ! lookup (Printer::printer, "raw", false))
	    {
	// table entry
		items = add (items, `item (`id (`settings), _("Printing filter settings"), ""));
		if (Printer::spooler != "cups")
		{
	// table entry
		    items = add (items, `item (`id (`ascii), _("ASCII printing settings"), ""));
		}
		if (Printer::spooler != "lprng")
		{
	// table entry
		    items = add (items, `item (`id (`perm), _("Restrictions settings"), ""));
	// table entry
		    items = add (items, `item (`id (`state), _("State and banners settings"), ""));
		}
	    }
	}

	term contents = `VBox (
	    `VSpacing (1),
	    `HBox (
		`HSpacing (1),
		`VBox (
			// check box
		    `Left (`CheckBox (`id (`yast), `opt (`notify), _("&Allow editing of this queue"), conf_type)),
		    `VSpacing (1),
			// table header
		    `Table (`id (`options), `opt(`keepSorting), `header ( _("Option area"),
			// table header
			 _("Current values")), items),
		    `HBox (
			// push button
			`PushButton (`id (`edit), `opt (`key_F4), EditButtonLabel ()),
			`HStretch (),
			// push button
			"class" == uri_type ? `HSpacing (0.1) : `PushButton (`id (`test), `opt (`key_F6), _("&Test"))
		    )
		),
		`HSpacing (1)
	    ),
	    `VSpacing (1)
	);

	// dialog caption
	Wizard::SetContentsButtons (_("Edit configuration"), contents, getEditHelp (uri_type, raw, lookup (Printer::printer, "ownppd", "") != ""),
	    BackButtonLabel (), OKButtonLabel ());
	Wizard::RestoreBackButton ();
    }

    /**
     * Complex edit dialog for printer...
     * @return symbol for wizard seq.
     */
    global define symbol runEditDialog () ``{
	Printer::edit_seq = true;
	tmp_printer_manuf_model = $[];
	any ret = nil;
	if (Printer::printer["database"]:nil == "foomatic")
	{
	}
	else
	    Printer::changeDb (lookup (Printer::printer, "database", nil));
	createEditDialog ();
	if (lookup (Printer::printer, "type", "yast2") == "yast2")
	{
	    UI::ChangeWidget (`id (`test), `Enabled, true);
	    UI::ChangeWidget (`id (`edit), `Enabled, true);
	}
	else
	{
	    UI::ChangeWidget (`id (`test), `Enabled, false);
	    UI::ChangeWidget (`id (`edit), `Enabled, false);
	}

	while (nil == ret)
	    {
		ret = UI::UserInput ();
		if (`edit == ret)
		{
		    ret = UI::QueryWidget (`id (`options), `CurrentItem);
		}
		if (`test == ret)
		{
		    ret = nil;
		    testPrinter (Printer::printer, -1);
		}
		else if (`abort == ret || `cancel == ret)
		{
		    ret = reallyAbort () ? `abort : nil;
		}
		else if (`yast == ret)
		{
		    boolean state = UI::QueryWidget (`id (`yast), `Value);
		    if (! state)
		    {
			// popup
			if (UI::YesNoPopup (_("If you forbid editing this queue,\nyou will not be able to change its settings\nusing YaST2. Continue?")))
			{
			    Printer::printer = add (Printer::printer, "type", "non-yast-now");
			    ret = `update;
			}
			else
			{
			    UI::ChangeWidget (`id (`yast), `Value, true);
			    ret = nil;
			}
		    }
		    else if (lookup (Printer::printer, "type", "yast2") == "non-yast-now"
			|| lookup (Printer::printer, "type", "yast2") == "yast2")
		    {
			Printer::printer = add (Printer::printer, "type", "yast2");
			ret = `update;
		    }
		    else
		    {
			// popup
			if (UI::YesNoPopup (_("The selected queue was not configured 
using YaST2. Do you want to add a printer 
with a prefilled name?
")))
			    ret = `add;
			else
			    ret = nil;
		    }
		}
		else if (`connection == ret)
		{
		    string uri = lookup (Printer::printer, "uri", "");
		    string uri_type = Printerlib::getUriType (uri);
		    ret = lookup ($["samba": `connection_samba, "novell": `connection_novell, "prefilter": `connection_prefilter, "filtering": `connection_filtering,
			"ipp": `ipp, "socket": `socket, "lpd": `connection_remote, "pipe" : `connection_pipe ], uri_type, `connection);
		}
		else if (`perm == ret)
                {
                    break;
                }
		else if (`state == ret)
                {
		    break;
                }
		else if (`ascii == ret)
		{
		    break;
		}
		else if (`config == ret)
		{
		    break;
		}
		else if (`ppd == ret)
		{
		    boolean cont = true;
		    if (Printer::spooler != "cups")
			cont = UI::YesNoPopup (getPpdWarning ());
		    ret = cont ? `ppd : nil;
		}
		if (`update == ret)
		{
		    if (lookup (Printer::printer, "type", "yast2") == "yast2")
		    {
			UI::ChangeWidget (`id (`test), `Enabled, true);
			UI::ChangeWidget (`id (`edit), `Enabled, true);
		    }
		    else
		    {
			UI::ChangeWidget (`id (`test), `Enabled, false);
			UI::ChangeWidget (`id (`edit), `Enabled, false);
		    }
		    ret = nil;
		}
	    }
	if (ret == `back || ret == `nexy || ret == `abort)
	    Printer::edit_seq = false;
	return ret;
    }

    /**
      * Start dialog with advanced spooler settings
      * @return symbol for wizzard seq
      */
    global define symbol runAdvancedDialog () ``{
	string new_spooler = Printer::spooler;
	integer new_cups_i = Printer::cupsInstallationType ();
	if (new_spooler == "cups" && Printer::cupsInstallationType () != 0)
	    new_spooler = "cups-client";

	term contents = `VBox (
	    `VSpacing (1),
	    `HBox (
		`HSpacing (1),
		`VBox (
		    `VStretch (),
		    `HBox (
			`HStretch (),
			// frame
			`Frame (_("Spooler"),
			    getSpoolerItems (new_spooler, false, Printer::db)
		        ),
			`HStretch ()
		    ),
		    `VStretch (),
		    // checkbox
		    `CheckBox (`id (`save), _("&Force saving everything"), Printer::save_all),
		    `VStretch (),
		    `HBox (`HWeight (999, `HStretch ()), `VBox (
		    // pushbutton
		    `PushButton (`id (`reinstall), `opt (`hstretch), _("Re&install printing packages")),
		    `VStretch (),
		    // pushbutton
		    `PushButton (`id (`scratch), `opt (`hstretch), _("Cl&ear current configuration")),
		    Printer::spooler == "cups" && 0 == Printer::cupsInstallationType () ? `VStretch () :`VSpacing (0),
		    // pushbutton
		    Printer::spooler == "cups" && 0 == Printer::cupsInstallationType () ? `PushButton (`id (`cupsd), `opt (`hstretch), _("CUPS server se&ttings")) : `VSpacing (0),
                    Printer::spooler == "cups" && 0 == Printer::cupsInstallationType () ? `VStretch () :`VSpacing (0),
                    // pushbutton
                    Printer::spooler == "cups" && 0 == Printer::cupsInstallationType () ? `PushButton (`id (`cupsusb), `opt (`hstretch), _("CUPS &USB Device Settings")) : `VSpacing (0),


		    `VStretch ()
		    ), `HWeight (999, `HStretch ()))
		),
		`HSpacing (1)
	    ),
	    `VSpacing (1)
	);

	// dialog caption
        Wizard::SetContentsButtons (_("Edit configuration"), contents, getAdvancedHelp (Printer::spooler == "cups" && 0 == Printer::cupsInstallationType ()),
	    BackButtonLabel (), NextButtonLabel ());
	Wizard::RestoreBackButton ();
	symbol ret = nil;
	while (nil == ret)
	{
	    ret = UI::UserInput ();
            if (ret == `cancel)
                ret = `abort;
	    if (ret == `scratch || ret == `reinstall || ret == `cupsusb)
	    {
		if (Mode::config)
	        {
	            DisplayUnavailableAIPopup ();
	            ret = nil;
	        }
	    }
	    if (`scratch == ret)
	    {
		// popup
		if (! UI::AnyQuestionPopup (WarningMsg(), _("Really discard your configuration?"),
			YesButtonLabel(), NoButtonLabel (), `focus_no))
		{
		    ret = nil;
		    continue;
		}
		Printer::Reset ();
		ret = `next;
		break;
	    }
	    else if (`reinstall == ret)
	    {
		if (Mode::cont)
		{
		    DisplayUnavailableAIPopup ();
		    continue;
		}
		// popup
		if (! UI::AnyQuestionPopup(WarningMsg(), _("Really reinstall your printing system? 
All your changes will be lost.
"),
		    // pushbutton
		    _("&Reinstall"), CancelButtonLabel (), `focus_no))
		{
		    ret = nil;
		    continue;
		}
		boolean success = reinstallAll ();
		break;
	    }
	    else if (`cups_server == ret || `foomatic == ret
		|| `cups_client == ret || `lprng == ret)
	    {
		symbol spooler = UI::QueryWidget (`id (`spooler), `CurrentButton);
		string spoolerstring = spooler == `lprng ? "lprng" : (spooler == `cups_client ? "cups-client" : "cups");
		if (spoolerstring != new_spooler)
		    UI::ChangeWidget (`id(`next), `Label, AcceptButtonLabel ());
		else
		    UI::ChangeWidget (`id(`next), `Label, NextButtonLabel ());
	    }
	    else if (`back == ret)
		break;
	    else if (`next == ret)
		break;
	    else if (`abort == ret)
	    {
		if (reallyAbort ())
		    break;
	    }
	    else if (`cupsd == ret)
		break;
	    else if (`cupsusb == ret)
	    {
		break;
	    }
	    ret = nil;

	}
	if (`next == ret)
	{
	    Printer::save_all = UI::QueryWidget (`id (`save), `Value);
	    symbol spooler = UI::QueryWidget (`id (`spooler), `CurrentButton);
	    if (`cups_server == spooler || `foomatic == spooler)
	    {
                Printer::server_hostname = "";
                if (Printerlib::getSpoolSystem () != 2)
                {
                    Printer::saveClientHostName ();
//                    Printerlib::switchTo ("cups");
                    ret = `switch;
                    Printer::cups_installation = -1;
		    restartSpoolerIfNeeded (true);
                }
                if (Printer::read_done == false)
                {
                    if (!Printer::Read ())
                    {
                        // message box
                        UI::MessagePopup (_("ERROR: Unable to read
the current configuration.
"));
                    }
                }
		Printer::cups_installation = 0;
		Printer::saveClientHostName ();
		restartSpoolerIfNeeded (true);
		ret = `cups;
		new_spooler = "cups";
		Printer::db = `foomatic == spooler ? `foomatic : `suse;
	    }
	    else if (`cups_client == spooler)
	    {
		 Printer::cups_installation = 1;
		 ret = `cups;
		 new_spooler = "cups";
	    }
	    else if (`lprng == spooler)
	    {
		Printer::db = `suse;
		new_spooler = "lprng";
	    }
	    else
	    {
		new_spooler = Printer::spooler;
	    }
	    if (Printer::spooler != new_spooler)
	    {
		Printer::save_all = true;
		Printer::reread = false;
		Printerlib::switchTo (new_spooler);
		return `switch;
	    }
	}
	return ret;
    }

    /**
      * Dialog for choosing what kind of devices to use
      * @return wizard sequencer symbol
      */
    global define symbol runCupsUsbDeviceTypeDialog () ``{
        term contents = `HBox (`HStretch (), `VBox (
            `VStretch (),
            `RadioButtonGroup (`id (`devtype),
			// frame
                `Frame (_("Use CUPS-Like USB Device Names"),`HBox (
                    `HSpacing (3),
                    `VBox (
                        `VSpacing (3),
				// radiobutton
                        `Left (`RadioButton (`id (`always), _("&Always"))),
                        `Left (`RadioButton (`id (`serial),
				// radiobutton
                            _("If &Serial Number is Known"))),
			// radiobutton
                        `Left (`RadioButton (`id (`never), _("&Never"))),
                        `VSpacing (3)
                    ),
                    `HSpacing (3)
                )
            )),
            `VStretch ()
        ), `HStretch ());
	// dialog caption
	Wizard::SetContentsButtons (_("CUPS USB Device Name Type"), contents,
	    getCupsUsbDeviceTypeHelp (), BackButtonLabel (), OKButtonLabel ());

	UI::ChangeWidget (`id (`devtype), `CurrentButton,
	    Printer::cups_usb_dev_names);
	ret = nil;
	while (ret == nil)
	{
	    ret = UI::UserInput ();
            if (ret == `cancel)
                ret = `abort;
	    if (ret == `abort)
	    {
		if (! reallyAbort ())
		    ret = nil;
	    }
	    else if (ret == `next)
	    {
		Printer::cups_usb_dev_names
		    = UI::QueryWidget (`id (`devtype), `CurrentButton);
	    }
	}
	return ret;
    }


    /**
     * Confirmation of an autodetected parallel/USB printer. User can skip it or
     * manually configure.
     * @return symbol for ws
     */
    global define symbol runProbedPrinterDialog () ``{
	y2debug ("Configuring printer %1", Printer::printer);
	if (Printer::confmode == `adm)
	{
	    y2debug ("adm mode, going connection setting");
	    return `connection;
	}
	// state variables
	boolean already_configured = false;
	boolean modelsel_needed = false;
	boolean configsel_needed = false;
	boolean devsel_needed = false;

	/* The end of the definitions */
	string uri = lookup (Printer::printer, "uri", "");
	string device = Printerlib::getUriDevice (uri);
	map info = $[];
	list detected = [];
	if (Printer::db != `foomatic)
	{
	    detected = SCR::Read (.printerdb.detect, [ lookup (Printer::printer, "vendor_ieee", ""), lookup (Printer::printer, "device_ieee", "") ]);
	}

	if (isPrinterConfigured (Printer::printer))
	    already_configured = true;

	if ((Printer::printer["bus"]:"" == ""
	    || Printer::printer["dev_name"]:"" == "")
	    && (Printer::printer["uri"]:"" == ""
	    || Printer::printer["uri"]:"" == "parallel:"))
	{
	    devsel_needed = true;
	}

	if (Printer::db == `foomatic)
	{
	    Printer::printer["vendor"] = Printer::printer["vendor_ieee"]:"";
	    Printer::printer["device"] = Printer::printer["device_ieee"]:"";

	    Printer::printer["vendor_db"] = SCR::Read (.ppd.db.vendorname,
		Printer::printer["vendor_ieee"]:"");
	    Printer::printer["device_db"] = SCR::Read (.ppd.db.modelname, [
                Printer::printer["vendor_db"]:"",
		Printer::printer["device_ieee"]:""]);

	    info = $[ "type" :
		Printer::foomatic[Printer::printer["vendor_db"]:"",
		    Printer::printer["device_db"]:"", `support]:1];
	    if (size (filter (`k, `v, Printer::foomatic[
		Printer::printer["vendor_db"]:"",
		Printer::printer["device_db"]:""]:$[],
		``(is (k, string)))) == 0)
	    {
		info["type"] = 2;
	    }

	    Printer::printer["database"] = "foomatic";
	}
	else if (2 == size (detected))
	{
	    info = SCR::Read (.printerdb.info, detected);
	    Printer::printer = union (Printer::printer, $[
		    "vendor" : select (detected, 0, ""),
		    "device" : select (detected, 1, ""),
		    ]);
	}

	y2debug ("Printer info: %1", info);

//	}
	if (2 == lookup (info, "type", 2) || 1 == lookup (info, "type", 2))
	{
	    if (2 == lookup (info, "type", 2))
	    {
		modelsel_needed = true;
	    }
	    // problematic support
	    y2debug ("Queue problematic, using configsel mode");
	    configsel_needed = true;

	    string comment = lookup (info, "printer", lookup (info, "config", ""));
	    if (comment!="")
	    {
	    }
	}
	//if no spooler installed, disable quick
	if (Printer::spooler != "cups" && Printer::spooler != "lprng")
	{
	}

	if (Printer::db == `foomatic)
	{
	    map PPDs = Printer::foomatic[
		Printer::printer["vendor_db"]:"",
		Printer::printer["device_db"]:""]:$[];
	    PPDs = filter (`k, `v, PPDs, ``(is (k, string)));
	    if (size (PPDs) < 1)
	    {
		// No PPD file available
		modelsel_needed = true;
	    }
	}
	else
	{

	  //if not all defaults available, disable quick
	  list default_queues = SCR::Read (.printerdb.auto, lookup (Printer::printer, "device", ""));
	  list default_configs = maplist (`e, default_queues, ``(lookup (e, "config", "")));
	  default_configs = filter (`e, default_configs, ``(e != ""));
	  list all_configs = SCR::Read (.printerdb.configs, lookup (Printer::printer, "device", ""));
	  all_configs = filter (`e, all_configs, ``(e != ""));

	  boolean all_cups = true;
	  boolean any_cups = false;
	  boolean all_lprng = true;
	  boolean any_lprng = false;

	  foreach (`c, all_configs, ``{
	    map info = SCR::Read(.printerdb.configinfo, c);
	    boolean cups = lookup (info, "cups", true);
	    boolean lprng = lookup (info, "lprng", true);
	    if (cups)
		any_cups = true;
	    else if (contains (default_configs, c))
	    {
		all_cups = false;
	    }
	    if (lprng)
		any_lprng = true;
	    else if (contains (default_configs, c))
	    {
		all_lprng = false;
	    }
	  });
	  if (! (all_cups && all_lprng))
	  {
	    configsel_needed = true;
	  }
	  if (Printer::spooler == "cups")
	  {
	    if (!any_cups)
	    {
		manufsel_needed = true;
	    }
	  }
	  else if (Printer::spooler == "lprng")
	  {
	    if (!any_lprng)
	    {
		modelsel_needed = true;
	    }
	  }
	}
/*	y2error ("Exists: %1, CfgNeed: %2, ModelNeed: %3, DeviceNeed: %4",
	        already_configured,
	        modelsel_needed,
	        configsel_needed,
		devsel_needed);
*/
	    if (already_configured)
	    {
		y2debug ("Already configured queue");
		ret = `queue;
	    }
	    else if (devsel_needed)
	    {
		y2debug ("Setting printer connection");
		ret = `connection;
	    }
	    else if (modelsel_needed)
	    {
		y2debug ("Running model selection");
		Printer::printer["modelset_required"] = true;
		ret = `model;
	    }
	    else if (configsel_needed)
	    {
		y2debug ("Starting single queue wz");
		ret = `queue;
	    }
	    else
	    {
		y2debug ("Going quick configuration");
		ret = `quick;
	    }
	    if (Printer::spooler == "cups")
	    {
		Printerlib::setCupsUsbDevicesInfo ();
		list problematic = Printerlib::getProblematicCupsUsbDevices ();
		string device = Printerlib::getUriDevice (
		    Printer::printer["uri"]:"");
		if (contains (problematic, device))
		    ret = `connection;
	    }
	return ret;
    }

    /**
     * Popup wanting device name.
     * @param pre_filled TextEntry is pre-set by this value.
     * @return string new device or ""
     */
    global define string getDeviceName (string pre_filled)``{
	symbol ret = `ok;
	UI::OpenDialog (
	    `opt (`decorated),
	    `VBox (
		`TextEntry (`id (`text),
			    // textentry label
			    _("&Enter device:"),
			    pre_filled),
		`HBox (
		    `PushButton (`id (`ok), `opt (`key_F10), OKButtonLabel ()),
		    `PushButton (`id (`cancel), `opt (`key_F9), CancelButtonLabel ())
		    )
		)
	    );
	ret = UI::UserInput ();
	pre_filled = UI::QueryWidget (`id (`text), `Value);
	UI::CloseDialog ();
	if (`cancel == ret)
	    {
		return "";
	    }
	return pre_filled;
    }



    /**
      * Backup list of print queues. See description of map printer
      */
    global list printcap_backup = nil;
    /**
      * Backup detected printer map
      */
    global map printer_backup = nil;
    /**
      * Newely created queue
      */
    global map new_added_queue = $[];
    /**
      * Backup of printer URI
      */
    global string uri_backup = nil;
    /**
      * The dialog is running again for repropose
      */
    global boolean running_again = false;

    /**
     * Show printer details in one window
     * @return symbol `back `next `abort
     */
    global define symbol runSinglePrinterDialog () ``{
	list forbidden_names = Printer::getForbiddenNames ();
	list new_queues = [];
	list queues = [];
	string first_queue = nil;
	string default_queue = nil;
	string uri = Printer::printer["uri"]:"";
	string vendor = Printer::printer["vendor"]:"";
	string device = Printer::printer["device"]:"";

	y2debug ("Single printer dialog: cap: %1,\nter: %2\nuri: %3\nagain: %4", printcap_backup, printer_backup, uri_backup, running_again);
	y2debug ("Single printer dialog: Real: cap: %1\nter: %2\n", Printer::printcap, Printer::printer);

	list mb_items = [
		// menubutton item
	    `item (`id (`clear), _("&Remove Everything")),
		// menubutton item
	    `item (`id (`again), _("Re&propose")),
	];
	boolean foomatic = false;
	if (Printer::printer["database"]:"unknown" != "unknown")
	    foomatic = Printer::printer["database"]:"" == "foomatic";
	else
	    foomatic = Printer::db == `foomatic;
	if (foomatic)
	{
	    mb_items = add (mb_items, `item (`id (`choose_ppd),
		// menubutton item
		_("&Choose PPD File")));
	}

	mb_items = add (mb_items, `item (`id (`choose_model),
	    // menubutton item
	    _("Choose &Model")));

        if (uri_backup != nil)
        {
            uri = uri_backup;
            uri_backup = nil;
        }

	list tmp_printcap = [];
	if (running_again)
	{
	    tmp_printcap = Printer::printcap;
	    Printer::printcap = [];
	}
	else
	{
	    Printer::additional_forbidden_names = [];
	}

	if (foomatic)
	{
	    string vendor = Printer::printer["vendor_db"]:"";
	    string model = Printer::printer["device_db"]:"";
	    string config = Printer::printer["config"]:nil;
	    y2debug ("Gettinq queues for %1", Printer::printer);
	    if (Printer::printer["ownppd"]:"" != "")
	    {
		model = Printer::printer["ownppd"]:"";
		vendor = "__ownppd__";
	    }
	    new_queues = getFoomaticAutoQueues (vendor, model, config);
	    default_queue = getFoomaticDefaultQueue (new_queues);
	}
	else
	{
	    string device = lookup (Printer::printer, "device", "");
	    new_queues = getSuSEdbAutoQueues (device);
	}

	if (running_again)
	{
	    Printer::printcap = tmp_printcap;
	}

	Printer::additional_forbidden_names = [];

	if (printcap_backup == nil)
	{
	    queues = getQueuesOfPrinter (uri, vendor, device);
	    new_queues = maplist (`q, new_queues, ``{
		q["new"] = true;
		return q;
	    });
	    if (size (queues) == 0 || nil == queues || running_again)
	    {
		queues = new_queues;
	    }
	}
	else
	{
	    queues = Printer::printcap;
	    Printer::printcap = printcap_backup;
	    printcap_backup = nil;
	}

	if (printer_backup != nil)
	{
	    Printer::printer = printer_backup;
	    printer_backup = nil;
	}

	if (new_added_queue != $[])
	{
	    queues = add (queues, new_added_queue);
	    new_added_queue = $[];
	}

	first_queue = nil;
	foreach (`q, queues, ``{
	    if (first_queue == nil)
	    {
		first_queue = q["name"]:nil;
	    }
	});

	running_again = false;

	// in-dialog text: To print, use: lpr QueueName filename
	term contents = `VBox (
	    `VSpacing (0.7),
	    `HBox (
		`HSpacing (2),
		// frame label
		`VBox (
		    `Heading (
			// heading label
			_("The following print queues have been configured:")
		    ),
		    `HBox (
			`HSpacing (1),
			`VBox (
			    `VSpacing (0.5),
			    `Table (
				// table header
				`id (`table), `header (_("Queue name"),
				// table header
				 _("Comment"),
				// table header
				_("New")),
				maplist (`v, queues, ``(`item (`id (v["name"]:""), v["name"]:"", v["comment"]:"", v["new"]:false ?
					// tabel entry
					_("Yes") :
					// table entry
					_("No"))))
				),
			    `VSpacing (0.5),
			    `HBox (
				`PushButton (`id (`add), `opt (`key_F3), AddButtonLabel ()),
				`PushButton (`id (`edit), `opt (`key_F4), EditButtonLabel ()),
				`PushButton (`id (`delete), `opt (`key_F5),
				    DeleteButtonLabel ()),
				`HStretch (),
				// pushbutton
				`PushButton (`id (`test), `opt (`key_F6), _("T&est Printing"))
			    ),
			    `VSpacing (0.5)
			    ),
			`HSpacing (1)
			)
		    ),
		    `HSpacing (2)
	        ),
	        `VSpacing (1),
                `HBox (
                    `HStretch (),
                    // menubutton
		    `MenuButton (_("E&xpert Settings"), mb_items),
		    `HStretch ()
                ),
	        `VSpacing (1)
	    );
	// label of the dialog
	Wizard::SetContentsButtons (_("Printer overview"), contents, getShowPrinterHelp (), UI::BackButtonLabel (), UI::OKButtonLabel ());

	UI::ChangeWidget (`id (`table), `CurrentItem, first_queue);

	symbol ret = nil;
	while (true)
	{
	    ret = UI::UserInput ();
	    if (`abort == ret || `cancel == ret)
	    {
		ret = `abort;
		if (reallyAbort ())
		{
		    break;
		}
	    }
	    else if (`test == ret)
	    {
		string i = UI::QueryWidget (`id (`table), `CurrentItem);
		foreach (`e, queues, ``{
		    if (lookup (e, "name", "") == i)
			testPrinter (e, -1);
		});
	    }
	    else if (ret == `clear || ret == `again || ret == `choose_ppd
		|| ret == `choose_model)
	    {
		// yes-no popup 1/3
		string warning = _("Warning:

All listed queues will be removed.
");
		if (ret == `again)
			// yes-no popup 2/3, optional
		    warning = warning + _("\nDefault PPD file will be used
for proposing new queues.");
		// yes-no popup 3/3
		warning = warning + _("\nContinue?");
		if (! UI::YesNoPopup (warning))
		{
		    ret = nil;
		    continue;
		}
		if (ret == `clear)
		{
		    queues = [];
		    UI::ChangeWidget (`id (`table), `Items, []);
		}
		else
		{
		    break;
		}
	    }
	    else if (ret == `edit)
	    {
		string i = UI::QueryWidget (`id (`table), `CurrentItem);
                if (i == nil)
                    continue;
		else
		    break;
	    }
	    else if (ret == `delete)
	    {
                string i = UI::QueryWidget (`id (`table), `CurrentItem);
		if (i == nil)
		    continue;
		if (UI::YesNoPopup (sformat (
		    // yes-no popup, %1 is replaced by printer name
		    _("Really delete the \"%1\" printer?"), i)))
		{
		    queues = filter (`q, queues, ``(
			q["name"]:"" != i
		    ));
		    UI::ChangeWidget (`id (`table), `Items, maplist (`v, queues, ``(`item (`id (v["name"]:""), v["name"]:"", v["comment"]:"",
			// table entry
			v["new"]:false ? _("Yes")
			// table entry
			: _("No")))));
// TODO		    UI::ChangeWidget (`id (`table), `CurrentItem,
		}
	    }
	    else
	    {
		break;
	    }
	}
	if (`next == ret)
	{
	    list(string) queue_names = maplist (`q, queues, ``(q["name"]:""));
	    list(string) orig_names = maplist (`q,
		getQueuesOfPrinter (uri, vendor, device),
		``(q["name"]:""));
	    Printer::printcap = filter (`p, Printer::printcap, ``(
		contains (queue_names, p["name"]:"")
		|| ! contains (orig_names, p["name"]:"")
	    ));
	    boolean set_default = Printer::default == ""
		|| Printer::default == nil;
	    // add queues...
	    foreach (`v, queues, ``{
		    Printer::selectPrinterByName (v["name"]:"");
		    Printer::printer = v;
		    Printer::storePrinter ();
		});
	    if (foomatic && set_default
		&& default_queue != nil && default_queue != "")
	    {
		Printer::default = default_queue;
	    }
	}
	else if (ret == `add || ret == `edit)
	{
	    Printer::additional_forbidden_names = Printer::getForbiddenNames ();
	    printer_backup = Printer::printer;
	    printcap_backup = Printer::printcap;
	    Printer::printcap = queues;
	    uri_backup = uri;
	    if (ret == `add)
	    {
		Printer::index = -1;
		Printer::printer["new"] = true;
		if (haskey (Printer::printer, "name"))
		    Printer::printer = remove (Printer::printer, "name");
		return `add;
	    }
	    else // edit
	    {
		string i = UI::QueryWidget (`id (`table), `CurrentItem);
		Printer::selectPrinterByName (i);
		return `edit;
	    }

	}
	else if (ret == `choose_ppd || ret == `again || ret == `choose_model)
	{
	    list tmp_printcap = Printer::printcap;
	    list deleted_queues = maplist (`q, queues, ``(q["name"]:""));
	    Printer::printcap = filter (`p, Printer::printcap, ``(
		! contains (deleted_queues, p["name"]:"")));
	    Printer::additional_forbidden_names = Printer::getForbiddenNames ();
	    Printer::additional_forbidden_names = filter (`n,
		Printer::additional_forbidden_names,
		``( ! contains (Printer::sys_forbidden_names, n)
	    ));
	    Printer::printcap = tmp_printcap;
	    if (ret == `again && haskey (Printer::printer, "config"))
		Printer::printer = remove (Printer::printer, "config");
	    running_again = true;
	}
	return ret;
    }

    /**
     * Create contents of table of autodetected printers.
     * @param current_sel currently selected item
     * @return list list of items, `ids are indexes to autoprobed_printers list
     */
    global define list getAutoprobedSelectionBox (integer current_sel) ``{
	integer current = -1;
	boolean selected = false;
	list items = maplist (`i, Printer::autodetected, ``{
	    current = current + 1;
	    if (current == current_sel)
	    {
		selected = true;
	    }
	    return `item (`id (current),
		isPrinterConfigured (i)
			// table entry, eg. Queue for Epson Stylus Photo on First parallel port (/dev/lp0)
			? sformat (_("Queue for %1 %2 on %3"), lookup (i, "vendor", ""), lookup (i, "device", ""),
				Printerlib::getDeviceNiceName (lookup (i, "dev_name", "")))
			  // Translators: "... on ...": will be st. like: Canon BJC-6100 on first parallel port.
			: sformat (_("%1 %2 on %3"), lookup (i, "vendor", ""), lookup (i, "device", ""),
			      Printerlib::getDeviceNiceName (lookup (i, "dev_name", ""))),
			  current == current_sel);
	});
	// selection box entry
	return add (items, `item (`id (-1), _("Other (not detected)"), !selected));
    }

    /**
     * Restart detection of printers.
     * @return symbol always `next
     */
    global define symbol runProbeDialog () ``{
	Printer::Detect ();
	return `next;
    }
    /**
     * Common detected hardware dialog.
     * @return symbol for ws
     */
    global define symbol runDetectedDialog () ``{
	Printer::confmode = `det;
	term contents = Wizard_hw::DetectedContent (
	    // Selectionbox caption
	    _("Printers to configure"),
	    getAutoprobedSelectionBox (runAutoprobedListDialog__current),
	    true,
	    // richtext caption
	    _("<P>Already installed printers and queues:</P>")
	    + Printer::Summary ([`nonew, `test])
	    );
	// dialog label
	Wizard::SetContentsButtons (_("Printer setup: Autodetected printers"), contents, getDetectedHelp (), BackButtonLabel (), FinishButtonLabel ());
//	Wizard::ReplaceBackButton(`VSpacing (0));
	any ret = nil;
	while (true)
	{
	    ret = UI::UserInput ();
            if (ret == `cancel)
                ret = `abort;
	    if (is (ret, string))
	    {
		// request for testing printer...
		Printer::selectPrinterByName (ret);
		testPrinter (Printer::printer, -1);
		continue ;
	    }
	    if (`abort == ret)// || `back == ret || `cancel == ret)
	    {
		ret = `abort;
		if (reallyAbort ())
		{
		    break;
		}
		continue;
	    }
	    if (`back == ret || `cancel == ret)
	    {
		ret = `back;
		if (reallyExit ())
		    break;
		continue;
	    }
	    else if (`next == ret) // finish
	    {
		// message box
		if (Printer::warn_finish && !UI::YesNoPopup (_("Now the changes to your printing 
system will be saved.
")))
		{
		    continue;
		}
	    }
	    else if (`configure_button == ret)
	    {
		Printer::conf_detected = true;
		runAutoprobedListDialog__current = UI::QueryWidget (`id (`detected_selbox), `CurrentItem);
		runProbedPrinterDlg__current = `none;
// comments added to go with other printer through the same sequence
//		if (-1 != runAutoprobedListDialog__current)
//		{
		    Printer::editDetected (select (Printer::autodetected, runAutoprobedListDialog__current, $[]));
//		}
//		else
//		{
//		    Printer::selectPrinter (-1);
//		    ret = `add;
//		}
	    }
	    break;
	}
	return ret;
    }
    /**
     * Complex dialog.
     * @return symbol for ws
     */
    global define symbol runComplexDialog () ``{
	Printer::confmode = `adm;
	string spoolerlabel = "";
	if (Printer::spooler == "cups")
	{
	    spoolerlabel = "CUPS";
	}
	else if (Printer::spooler == "lprng")
	{
	    spoolerlabel = "LPRng";
	}
	else
	    spoolerlabel = "any spooler";
	Wizard::SetContentsButtons (
	// Header of the dilag with all the printers
	    sformat (_("Printer administration for %1"), spoolerlabel),
	    Wizard_hw::ConfiguredContent (
		(Printer::spooler == "cups" || Printer::spooler == "lprng")
		?
			// table header
		    `header (_("Default"),
			// table header
			 _("Name"),
			// table header
			 _("Type"),
			// table header
			 `Center(_("Ready")))
		:
			// table header
		    `header (_("Default"),
			// table header
			 _("Name"),
			// table header
			 _("Type"),
			// table header
			 `Center(_("CUPS")),
			// table header
			 `Center(_("LPRng"))),
		printcapTable (),
		nil, nil,
		`HBox (
		    `HStretch (),
		// push button
		`PushButton (`id (`setdefault), _("&Set as default")),
		// push button
		`PushButton (`id (`advanced), `opt (`key_F7), _("Ad&vanced")),

		    `HStretch ()
		),
		nil
	    ),
	    getComplexHelp (),
	    BackButtonLabel (),
	    FinishButtonLabel ()
	);
	if (Printer::detection)
	{
	    Wizard::RestoreBackButton ();
	}
	else
	{
//	    Wizard::ReplaceBackButton (`VSpacing (0));
	}
	UI::ChangeWidget (`id (`table), `CurrentItem, -1 == Printer::index ? 0 : Printer::index);
	symbol ret = nil;
	while (true)
	    {
		ret = UI::UserInput ();
	        if (ret == `cancel)
		    ret = `abort;

		if (ret == `add_button)
		{
		    Printer::selectPrinter (-1);
		    Printer::conf_detected = false;
		    return `add;
		}
		else if (ret == `edit_button || ret == `delete_button || ret == `setdefault)
		{
		    Printer::selectPrinter (AnyToInteger (UI::QueryWidget (`id (`table), `CurrentItem)));
		    if (-1 != Printer::index)
		    {
			if (ret == `edit_button)
			{
			    return `edit;
			}
			else if (`delete_button == ret)
			{
			    // message box, %1 is replaced by printer name
			    if (UI::YesNoPopup (sformat (_("Really delete the \"%1\" printer?
"), lookup (Printer::printer, "name", ""))))
				Printer::deletePrinter ();
			    UI::ChangeWidget (`id (`table), `Items, printcapTable ());
			    UI::ChangeWidget (`id (`table), `CurrentItem, Printer::index);
			}
			else
			{
			    Printer::setDefaultPrinter ();
			    UI::ChangeWidget (`id (`table), `Items, printcapTable ());
			    UI::ChangeWidget (`id (`table), `CurrentItem, Printer::index);
			}
		    }
		    else
		    {
			// message box
			UI::MessagePopup (_("Select a printer."));
		    }
		}
		else if (`advanced == ret)
		{
		    break;
		}
		else if (`cancel == ret || `abort == ret)
		{
		    ret = `abort;
		    if (reallyAbort ())
		    {
			break;
		    }
		}
		if (`back == ret)
		{
		    if (Printer::detection || reallyExit())
			break;
		}
		if (`next == ret)
		{
		    // message box
		    if (!Printer::warn_finish || UI::YesNoPopup (Printer::save_all ? _("Now the configuration of the printing 
system will be saved
")
			// message box
			: _("Now the changes to your printing \nsystem will be saved.")))
		    {
			break;
		    }
		}
		if (`scratch == ret)
		{
			// message box
		    if (! UI::AnyQuestionPopup(WarningMsg(), _("Really reinstall your printing system? 
All your changes will be lost.
"),
			// push button
			_("&Reinstall"), CancelButtonLabel (), `focus_no))
			    continue;
		    boolean success = reinstallAll ();
			break;
		}
	    }
	return ret;
    }
    /**
     * Revert the database back to the one passed on command line
     * @return next for wizard sequencer
     */
    global define symbol revertDbPopup () ``{
	Printer::revertDb ();
	if (Arch::s390)
	    return `next_net;
	else
	    return `next;
    }
    /**
      * Detect installation type
      * @return symbol `server or `client
      */
    global define symbol runType () ``{
	if (Printer::spooler != "cups")
	    return `server;
	if (Printer::cupsInstallationType () == 0)
	    return `server;
	return `client;
    }
    /**
      * Detect configuration type (class/database/ppd)
      * @return symbol `class or `db or `ppd
      */
    global define symbol runCType () ``{
	if (lookup (Printer::printer, "ownppd", "") != "")
	    return `ppd;
	if (lookup (Printer::printer, "uri", "") == "class")
	    return `class;
	return `db;
    }

    /**
      * Get configuration type for queue
      * @return symbol for wizard sequencer
      */
    global define symbol queueConfType () ``{
	y2debug ("Confmode is %1, utirype %2", Printer::confmode, Printer::getUriType ());
	if ("prefilter" == Printer::getUriType ())
	{
	    y2debug ("Prefilter queue, going single");
	    return `single;
	}

	symbol ret = `all;//Printer::confmode == `adm ? `all : `single;
	return ret;
    }

    /**
      * Store queue to temporary storage
      * @return symbol for wizard sequencer
      */
    global define symbol storeQueue () ``{
	new_added_queue = Printer::printer;
	return `next;
    }

    /**
      * Test whether at least one queue will be proposed
      * Get data from internal structures
      * @return symbol `yes or `no
      */
    global define symbol wasProposedQueue () ``{
        boolean foomatic = false;
        if (Printer::printer["database"]:"unknown" != "unknown")
            foomatic = Printer::printer["database"]:"" == "foomatic";
        else
            foomatic = Printer::db == `foomatic;

        string uri = Printer::printer["uri"]:"";
        string vendor = Printer::printer["vendor"]:"";
        string device = Printer::printer["device"]:"";

	list new_queues = [];
        if (foomatic)
        {
            string vendor = Printer::printer["vendor_db"]:"";
            string model = Printer::printer["device_db"]:"";
            string config = Printer::printer["config"]:nil;
            y2debug ("Gettinq queues for %1", Printer::printer);
            if (Printer::printer["ownppd"]:"" != "")
            {
                model = Printer::printer["ownppd"]:"";
                vendor = "__ownppd__";
            }
            new_queues = getFoomaticAutoQueues (vendor, model, config);
        }
        else
        {
            string device = lookup (Printer::printer, "device", "");
            new_queues = getSuSEdbAutoQueues (device);
        }

	list old_queues = getQueuesOfPrinter (uri, vendor, device);

	if (size (new_queues) > 0 || size (old_queues) > 0)
	{
	    return `yes;
	}
	return `no;
    }

    /**
     * Get map of aliases for wizard sequencer.
     * @return aliases
     */
    global define map getAliases () ``{
	 return $[
	    "type" :		[ ``(runType ()), true, ],
	    "c_type" :		[ ``(runCType ()), true, ],
	    "complex" :		``(runComplexDialog ()),
	    "advanced" :	``(runAdvancedDialog ()),
	    "cupsd" :		``(runCupsServerDialog ()),
            "cupsaclroot" :	``(runCupsAclDialog (`root)),
            "cupsacladmin" :	``(runCupsAclDialog (`admin)),
            "cupsaclprinters" :	``(runCupsAclDialog (`printers)),
            "cupsaclclasses" :	``(runCupsAclDialog (`classes)),
	    "cups_client" :	``(runClientDialog ()),
	    "connection" :	``(runConnectionDialog ()),
	    "advconnection" :	``(runAdvancedConnectionDialog ()),
	    "parallel" :	``(runDeviceDialog ("parallel")),
	    "file" :		``(runDiskFileDialog ("file")),
	    "pipe" :		``(runDiskFileDialog ("pipe")),
	    "serial" :		``(runDeviceDialog ("serial")),
	    "usb" :		``(runDeviceDialog ("usb")),
	    "irda" :		``(runDeviceDialog ("irda")),
	    "samba" :		``(runRemoteDialog ("samba")),
	    "novell" :		``(runRemoteDialog ("novell")),
	    "prefilter" :	``(runPrefilterDialog ()),
	    "chooseremote" :	``(runChooseRemoteDialog ()),
	    "remote" :		``(runRemoteDialog ("lpd")),
	    "filtering" :	``(runRemoteDialog ("filtering")),
	    "ipp" :		``(runRemoteDialog ("ipp")),
	    "socket" :		``(runRemoteDialog ("socket")),
	    "other" :		``(runUriDialog()),
	    "model" :		``(runManufModelDialog ()),
	    "inst_printer" :	``(runInstalledPrinterDialog ()),
	    "useppd" :		``(runSelectPpdFile ()),
	    "config" :		``(runConfigDialog (false)),
	    "ppdselect" :	``(runConfigDialog (true)),
	    "name" :		``(runNameDialog (false)),
	    "namerem" :		``(runNameDialog (false)),
	    "namedet" :		``(runNameDialog (true)),
	    "members" :		``(runMembersDialog ()),
	    "filter" :		``(runOptionsDialog ()),
	    "features" :	``(runFeaturesDialog ()),
	    "users" :		``(runUsersDialog ()),
	    "state" :		``(runStateDialog ()),
	    "edit" :		``(runEditDialog ()),
	    "detected" :	``(runDetectedDialog ()),
	    "conftype" :	[ ``(runProbedPrinterDialog ()), true],
	    "single" :		``(runSinglePrinterDialog ()),
	    "is_proposed" :	[ ``(wasProposedQueue ()), true ],
	    "redetect" :	[ ``(runProbeDialog ()), true ],
	    "storeprinter" :	[ ``(storePrinter ()) , true ],
	    "storequeue" :	[ ``(storeQueue ()), true ],
	    "ppdetails" :	[ ``(Printerlib::editParallelPort ()), true ],
	    "spdetails" :	[ ``(Printerlib::editSerialPort ()), true ],
	    "revertdb" :	[ ``(revertDbPopup ()), true ],
	    "queue_conf" :	[ ``(queueConfType ()), true ],
	    "main_wz" :		``(WizardSequencer (getAliases (), main_sequence)),
	    "add_printer_wz" :	``(WizardSequencer (getAliases (), add_printer_sequence)),
	    "edit_printer_wz" :	``(WizardSequencer (getAliases (), edit_printer_sequence)),
	    "cups_wz" :		``(WizardSequencer (getAliases (), cups_sequence)),
//	    "detected_wz" :	``(WizardSequencer (getAliases (), detected_sequence)),
	    "add_queue_wz":	``(WizardSequencer (getAliases (), add_queue_sequence)),
	    "add_queue_wz_1":	[ ``(WizardSequencer (getAliases (), add_queue_sequence)), true],
	    "single_printer_wz":``(WizardSequencer (getAliases (), single_printer_sequence)),
	    "cups_usb" :	``(runCupsUsbDeviceTypeDialog ()),
	    ];
    }
    /**
     * Show all dialogs with some fake data...
     */
    global define void testDialogs () ``{
	Printer::printcap = [
	    $["conf":$[], "config":"gs-stp-canon-bjc-6100-color-high", "device":"canon-bjc-6100", "device_ieee":"BJC-6100", "name":"best", "options":$["papersize-all-inkjet":1], "type":"yast2", "unique_key":"Kns9.VeHHAhw8MvA", "uri":"usb:/dev/usb/lp0", "vendor":"canon", "vendor_ieee":"Canon"],
	    $["conf":$[], "config":"gs-stp-canon-bjc-6100-color-low", "device":"canon-bjc-6100", "device_ieee":"BJC-6100", "name":"color", "options":$["papersize-all-inkjet":1], "type":"yast2", "unique_key":"Kns9.VeHHAhw8MvA", "uri":"usb:/dev/usb/lp0", "vendor":"canon", "vendor_ieee":"Canon"],
	    $["conf":$[], "config":"gs-stp-canon-bjc-6100-color-medium", "device":"canon-bjc-6100", "device_ieee":"BJC-6100", "name":"high", "options":$["papersize-all-inkjet":1], "type":"yast2", "unique_key":"Kns9.VeHHAhw8MvA", "uri":"usb:/dev/usb/lp0", "vendor":"canon", "vendor_ieee":"Canon"],
	    $["conf":$[], "config":"gs-stp-canon-bjc-6100-mono-low", "device":"canon-bjc-6100", "device_ieee":"BJC-6100", "name":"lp", "options":$["papersize-all-inkjet":1], "type":"yast2", "unique_key":"Kns9.VeHHAhw8MvA", "uri":"usb:/dev/usb/lp0", "vendor":"canon", "vendor_ieee":"Canon"],
	    $["conf":$[], "ff":false, "name":"rem", "raw":false, "type":"yast2", "uri":"lpd://printer.suse.de/lp"],
	];
	Printer::autodetected = [
	    $["bus":"usb", "dev_name":"/dev/usb/lp0", "device":"BJC-6100", "unique_key":"Kns9.VeHHAhw8MvA", "uri":"usb:/dev/usb/lp0", "vendor":"Canon"],
	];
	Printer::default = "lp";
	Printer::selectPrinter (3);
	list seq = [ "detected", "configdetected", "quick", "complex", "connection", "usb", "name", "model", "config", "filter", "featues", "edit", ];
	foreach (`i, seq, ``{
	    eval (lookup (getAliases (), i, nil));
	});
	Printer::index = -1;									runConnectionDialog ();
	Printer::printer = add (Printer::printer, "uri", "parallel:/dev/lp0");		runDeviceDialog ("parallel");
	Printer::printer = add (Printer::printer, "uri", "serial:/dev/ttyS0");		runDeviceDialog ("serial");
	Printer::printer = add (Printer::printer, "uri", "file:/tmp/printer.ps");		runDiskFileDialog ();
	Printer::printer = add (Printer::printer, "uri", "samba://user@smb.suse.de/lp");	runRemoteDialog ("samba");
	Printer::printer = add (Printer::printer, "uri", "novell://user@novell.suse.de/lp");runRemoteDialog ("novell");
	Printer::printer = union (Printer::printer, $[
		"create_remote_queue" : true,
		"remote_host" : "printer.suse.de",
		"remote_printer" : "lp",
		"uri": "prefilter://remote",
	]);										runPrefilterDialog ();
	Printer::printer = add (Printer::printer, "uri", "prefilter:/rem");			runChooseRemoteDialog ();
	Printer::printer = add (Printer::printer, "uri", "lpr://printer.suse.de/lp");	runRemoteDialog ("lpd");
    }
    /**
     * Run configuratin wizard.
     * @param skip_detection true if detection is skipped
     * @return `next, `back, `abort. If `next, then save settings.
     */
    global define symbol runWizard (boolean skip_detection) ``{
	map detected_sequence = $[
	    "ws_start" : "type",
	    "type" : $[`server : "detected", `client : "main_wz"],
	    "redetect" : $[`next : "detected", ],
	    "detected" : $[
		`next : `next,
		`abort : `abort,
		`configure_button : "add_printer_wz",
		`restart_button : "redetect",
		`edit_button : "main_wz",
		],
	    "add_printer_wz" : $[
		`abort : `abort,
		`next : "detected",
		],
	    "main_wz" : $[
		`next : `next,
		`abort : `abort,
		`switch : `switch,
		`reinstall : `reinstall,
		],
	    ];
	map main_sequence = $[
	    "ws_start" : "type",
	    "type" : $[`server : "complex", `client : "cups_client"],
	    "complex"  : $[
		`next : `next,
		`add  : "add_printer_wz",
		`edit : "edit_printer_wz",
		`abort : `abort,
		`client : "cups_client",
		`advanced : "advanced",
		],
	    "advanced" : $[
		`next : "type",
		`this : "advanced",
		`cups : "type",
		`cupsd : "cupsd",
		`cupsusb : "cups_usb",
		`reinstall : `reinstall,
		`switch : `switch,
		`abort : `abort,
		],
	    "cups_usb" : $[
		`next : "advanced",
		`abort : `abort,
	    ],
	    "cupsd" : $[
		`next : "advanced",
		`abort : `abort,
		`root : "cupsaclroot",
		`admin : "cupsacladmin",
		`printers : "cupsaclprinters",
		`classes : "cupsaclclasses",
	    ],
	    "cupsaclroot" : $[
		`next : "cupsd",
		`abort : `abort,
	    ],
            "cupsacladmin" : $[
                `next : "cupsd",
                `abort : `abort,
            ],
            "cupsaclprinters" : $[
                `next : "cupsd",
                `abort : `abort,
            ],
            "cupsaclclasses" : $[
                `next : "cupsd",
                `abort : `abort,
            ],
	    "add_printer_wz" : $[
		`next: "complex",
		`abort : `abort,
		],
	    "edit_printer_wz": $[
		`next: "complex",
		`abort : `abort,
		],
	    "cups_client" : $[
		`next : `next,
		`abort : `abort,
		`advanced : "advanced",
		`switch : `switch,
		],
	    ];
	map single_printer_sequence = $[
	    "ws_start" : "is_proposed",
	    "is_proposed" : $[`yes : "single", `no : "add_queue_wz_1"],
	    "single" : $[
		`abort : `abort,
		`next : `next,
		`edit : "edit_printer_wz",
		`add : "add_queue_wz",
		`again : "single",
		`choose_ppd : "ppdselect",
		`choose_model : "model",
	    ],
	    "edit_printer_wz" : $[ `abort : `abort, `next : "single"],
	    "add_queue_wz" : $[ `abort : `abort, `next : "single"],
	    "add_queue_wz_1" : $[ `abort : `abort, `next : "single"],
	    "ppdselect" : $[ `abort : `abort, `next : "single", ],
	    "model" : $[ `abort : `abort, `next : "ppdselect", `useppd : "useppd", ],
            "useppd" : $[ `abort : `abort, `db : "model", `next: "ppdselect" ],

	];
	map add_printer_sequence = $[
	    "ws_start"   : "revertdb",
	    "revertdb"   : $[ `next : "conftype", `next_net : "conftype" ],
	    "conftype" : $[
                `abort : `abort,
                `quick : "single_printer_wz",
                `model : "model",
		`connection : "connection",
		`queue : "add_queue_wz",
	    ],
	    "connection" : $[
		`parallel : "parallel",
		`serial: "serial",
		`usb: "usb",
		`irda: "irda",
		`file: "file",
		`more: "advconnection",
		`existing : "inst_printer",
		`abort : `abort,
		],
	    "advconnection" : $[
		`samba: "samba",
		`novell: "novell",
		`prefilter: "prefilter",
		`filtering: "filtering",
		`remote: "remote",
		`ipp: "ipp",
		`socket: "socket",
		`pipe: "pipe",
		`other: "other",
		`class : "add_queue_wz",
		`existing : "inst_printer",
		`abort : `abort,
	    ],
	    "parallel" : $[ `next : "model", `abort : `abort, `ppdetails : "ppdetails", ],
	    "file" : $[ `next : "model", `abort : `abort, ],
	    "pipe" : $[ `next : "model", `abort : `abort, ],
	    "serial" : $[ `next : "model", `abort : `abort,  `spdetails : "spdetails", ],
	    "usb" : $[ `next : "model", `abort : `abort, ],
	    "irda" : $[ `next : "model", `abort : `abort, ],
	    "samba" : $[ `next : "model", `abort : `abort, ],
	    "novell" : $[ `next : "model", `abort : `abort, ],
	    "prefilter" : $[ `next : "model", `abort : `abort, ],
	    "remote" : $[ `next : "add_queue_wz", `abort : `abort, ],
	    "filtering" : $[ `next : "model", `abort : `abort, ],
	    "ipp" : $[ `next : "model", `abort : `abort, ],
	    "socket" : $[ `next : "model", `abort : `abort, ],
	    "other" : $[ `next : "model", `abort : `abort, ],

	    "inst_printer" : $[ `next : "add_queue_wz", `abort : `abort, ],
	    "add_queue_wz" : $[ `next : "storeprinter", `abort : `abort, ],

	    "model" : $[ `abort : `abort, `next : "queue_conf", `useppd : "useppd", ],
	    "useppd" : $[ `abort : `abort, `db : "model", `next: "queue_conf" ],
	    "queue_conf" : $[ `abort : `abort, `all : "single_printer_wz", `single : "add_queue_wz" ],
	    "single_printer_wz" : $[ `abort : `abort, `next : `next ],

	    "ppdetails" : $[ `abort : `abort, `next : "parallel", ],
	    "spdetails" : $[ `abort : `abort, `next : "serial", ],
	    "storeprinter" : $[`next : `next, ],
	    ];


	map add_queue_sequence = $[
	    "ws_start" : "name",
            "name" : $[ `abort : `abort, `next : "config", `raw : "storequeue", `class : "members", ],
            "config" : $[ `abort : `abort, `advanced : "filter", `next: "storequeue", ],
            "filter" : $[ `abort : `abort, `next_cups : "cups_wz", `next : "features", ],
            "features" : $[ `abort : `abort, `next_cups : "cups_wz", `next : "config", ],
            "cups_wz" : $[ `abort : `abort, `next : "c_type", ],
            "c_type" : $[ `db : "config", `class : "members", `ppd : "config", ],
	    "members" : $[ `abort : `abort, `advanced : "cups_wz", `next : "storequeue", ],
	    "storequeue" : $[ `abort: `abort, `next: `next],
	];

	map edit_printer_sequence = $[
	    "ws_start"   : "edit",
	    "edit" : $[
		`connection_samba : "samba",
		`connection_novell : "novell",
		`connection_prefilter : "chooseremote",
		`connection_remote : "remote",
		`connection_filtering: "filtering",
		`connection_pipe: "pipe",
		`ipp : "ipp",
		`socket: "socket",
		`connection : "connection",
		`name : "name",
		`model : "model",
		`config : "config",
		`settings : "filter",
		`ascii : "features",
		`state : "state",
		`perm : "users",
		`indep : "features",
		`ppd : "useppd",
		`cups: "cups_wz",
		`class_members : "members",
		`class : "cups_wz",
		`add : "add_printer_wz",
		`next : "storeprinter",
		`abort : `abort,
		],
	    "features" : $[ `next : "edit", `next_cups : "edit", `abort : `abort, ],
	    "state" : $[ `next : "edit", `abort : `abort, ],
	    "users" : $[ `next : "edit", `abort : `abort, ],
	    "add_printer_wz" : $[ `next : "edit", `abort : `abort ],
	    "samba" : $[ `next : "edit", `abort : `abort, ],
	    "novell" : $[ `next : "edit", `abort : `abort, ],
	    "chooseremote" : $[ `next : "edit", `abort : `abort, ],
	    "remote" : $[ `next : "edit", `abort : `abort, ],
	    "ipp" : $[ `next : "edit", `abort : `abort, ],
	    "socket" : $[ `next : "edit", `abort : `abort, ],
	    "connection" : $[
		`parallel: "parallel",
		`serial : "serial",
		`usb : "usb",
		`irda : "irda",
		`file : "file",
		`abort : `abort,
		],
	    "parallel" : $[ `abort : `abort, `next : "edit", `ppdetails : "ppdetails", ],
	    "ppdetails" : $[ `abort : `abort, `next : "parallel", ],
	    "serial" : $[ `abort : `abort, `next : "edit", `spdetails : "spdetails",],
	    "usb" : $[ `abort : `abort, `next : "edit", ],
	    "irda" : $[ `next : "edit", `abort : `abort, ],
	    "filtering" : $[ `abort: `abort, `next: "edit", ],
	    "file" : $[ `abort : `abort, `next : "edit", ],
	    "pipe" : $[ `abort : `abort, `next : "edit", ],
	    "model" : $[ `abort : `abort, `next : "config", `useppd: "useppd",],
	    "config" : $[ `abort : `abort, `advanced : "filter", `next: "edit", ],
	    "name" : $[ `abort : `abort, `next : "edit", `raw : "edit", `class : "edit" ],
	    "filter" : $[ `abort : `abort, `next : "edit", `next_cups : "edit", ],
	    "useppd" : $[ `abort : `abort, `db : "model", `next: "edit" ],
	    "members" : $[ `abort : `abort, `next : "edit", ],
	    "cups_wz" : $[ `abort : `abort, `next : "edit", ],
	    "ppdetails" : $[ `abort : `abort, `next : "parallel", ],
	    "spdetails" : $[ `abort : `abort, `next : "serial", ],
	    "storeprinter" : $[`next : `next, ],
	    ];
	map cups_sequence = $[
	    "ws_start" : "users",
	    "users" : $[ `abot : `abort, `next : "state", ],
	    "state" : $[ `abort : `abort, `next : `next, `next_ppd : `next_ppd, ],
	    ];

	Printer::detection = !skip_detection;
	return WizardSequencer (getAliases (), skip_detection ? main_sequence : detected_sequence);
    }
}
