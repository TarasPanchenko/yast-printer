/**
 * File:
 *   modules/Spooler.ycp
 *
 * Package:
 *   Printing spooler library.
 *
 * Summary:
 *   Some utility functions for spooler settings
 *
 * Authors:
 *   Jiri Srain <jsrain@suse.cz>
 *
 * $Id$
 *
 * Printer library exported functions. What else?!?
 */

{
    module "Spooler";

    textdomain "printer";

    import "CUPS";
    import "LPRng";
    import "Mode";
    import "Label";
    import "Package";
    import "PackageAI";
    import "PackageSystem";
    import "Popup";
    import "Report";
    import "Service";

// local variables

    /**
      * Currently used spooler, cups or lprng
      */
    string spooler = "unknown";

    boolean are_drivers_installed = nil;

    global map<string,boolean> available_packages = $[];

    /**
     * Printer's name mustn't be the same as a name of one of the files
     * in the /etc/lpdfilter directory. These are the files. If it is nil,
     * the list hasn't been initialized yet. Use function
     * getSystemForbiddenNames for accessing this list.
     */
    list<string> sys_forbidden_names = nil;

// global variables

// local functions

    /**
      * display CUPS initialization popup
      */
    define void showCupsInitPopup () ``{
        // popup
        UI::OpenDialog(`opt(`decorated ),`VBox (
                // label
            `Label(_("Initializing CUPS server...
This usually takes one or two minutes")),
            `ProgressBar (`id (`cups_prog), "", 60 * 10)));
    }




// global functions

    /**
      * Get the currently used spooler
      * FIXME TODO if not known, detect
      * @return currently used spooler
      */
    global define string Get() ``{
	return spooler;
    }

    /**
      * Set the spooler that is used
      * FIXME high-level functions should be used instead of this one, this
      *       should get local
      * @param spooler_type string spooler that is used
      */
    global define void Set (string spooler_type) ``{
	spooler = spooler_type;
    }

    /**
      * get RadioButtonGroup for spooler selection
      * @param spooler string currently used spooler (cups, cups-client,
      *  lprng or current)
      * @param none boolean show leaving current status
      * @return term RadioButtonGroup widget
      */
    global define term getSpoolerItems (string spooler, boolean none) ``{
	boolean lprng_available = true;
	if (haskey (available_packages,"lprng"))
	{
	    lprng_available = available_packages["lprng"]:false;
	}
	else
	{
	    lprng_available = Package::Available ("lprng");
	    available_packages["lprng"] = lprng_available;
	}
	return `RadioButtonGroup (
	    `id (`spooler),
	    `HBox (`HSpacing (1), `VBox (
		`VSpacing (1),
		`RadioButton (`id (`cups_server),
		    `opt (`hstretch, `notify),
		    // radiobutton
		    _("CUPS full &server installation"),
		    spooler == "cups"),
		`VSpacing (0.5),
		`RadioButton (`id (`cups_client),
		    `opt (`hstretch, `notify),
		    // radiobutton
		    _("CUPS &client-only installation"),
		    spooler == "cups-client"),
		lprng_available ? `VSpacing (0.5) : `VSpacing (0),
		lprng_available
		    ? `RadioButton (`id (`lprng),
			`opt (`hstretch, `notify),
			// radiobutton
			_("&LPRng"),
			spooler == "lprng")
		    :`VSpacing (0),
		none ? `VSpacing (0.5) : `VSpacing (0),
		none ? `RadioButton (`id (`none),
		    `opt (`hstretch, `notify),
		    // radiobutton
		    _("L&eave current status"),
		    spooler == "current")
		    : `VSpacing (0),
		`VSpacing (1)
	     ), `HSpacing (1)
	));
    }

    /**
      * Shows a question to a user whether he wants the LPRng or CUPS spooler.
      * @param msg string to be displayed
      * @param abort_msg label of abort button
      * @return symbol specifying users selecion (lprng, cups, exit)
      */
    global define symbol chooseSpooler(string msg, string abort_msg)
    ``{
	symbol ret = `none_symbol;
	UI::OpenDialog (
            `VBox (
		`Label (msg),
		getSpoolerItems ("current", true),
		`HBox (
		    `HWeight (1, `PushButton (`id (`ok), `opt (`key_F10), Label::OKButton ())),
		    `HWeight (1, `PushButton (`id (`cancel), `opt (`key_F9), abort_msg))))
	);
	while (true)
	{
	    ret = (symbol)UI::UserInput ();
	    if (ret == `ok || ret == `cancel)
		break;
	}
	if (`ok == ret)
	{
	    ret = (symbol)UI::QueryWidget (`id (`spooler), `CurrentButton);
	}
	UI::CloseDialog();
	return ret;
    }

    /**
     * Get list of names that are forbidden because they are used as
     * filenames in /etc/lpdfilter
     * @return list of strings -- these strings are not allowed as queue
     * names.
     */
    global define list getSystemForbiddenNames () ``{
	if (nil == sys_forbidden_names)
	{
	    if (0 != SCR::Execute (.target.bash, "test -d /etc/lpdfilter"))
		sys_forbidden_names = [];
	    else
		sys_forbidden_names = (list<string>)SCR::Read (.etc.printcap.sysnames);
	    if (nil == sys_forbidden_names)
		sys_forbidden_names = [];
	}
	sys_forbidden_names
	    = maplist (string n, sys_forbidden_names, ``(tolower (n)));
	return sys_forbidden_names;
    }

    /**
      * stop currently running printer spooler service
      */
    global define void StopServices () ``{
	if (Mode::config)
	    return;
	SCR::Execute (.target.bash, "test -f /etc/init.d/lpd && /etc/init.d/lpd stop");
	SCR::Execute (.target.bash, "test -f /etc/init.d/cups && /etc/init.d/cups stop");
    }

    /**
     * Test which spooling system is most probably used.
     * @return integer with some bits set:<br>
     *    1 if lpd is installed<br>
     *    2 if cups is installed<br>
     *      (therefore 0 if none is installed)<br>
     *      (therefore 3 if both are installed)<br>
     *  256 is added if the instalation doesn't seem to be complete
     */
    global define integer getSpoolSystem () ``{
	// first try if some of the systems exists.
	boolean cups_exists = PackageSystem::Installed("cups-client");
	// for lpd we need to check the file because
	boolean lpd_exists = PackageSystem::Installed ("lprng");
	y2milestone ("CUPS exists: %1, LPRng exists: %2",
	    cups_exists, lpd_exists);
	boolean cups_complete = PackageSystem::InstalledAll (
	    [/*"cups-drivers", "cups-libs",*/ "cups", "cups-client",/*"filters",
	    "foomatic-filters"*/]);
	boolean lpd_complete = PackageSystem::InstalledAll (
	    [/*"lpdfilter", "filters", "cups-drivers", "foomatic-filters"*/]);

	if (cups_exists && !lpd_exists)
	    {
		if (cups_complete)
		    return 2;
		else
		{
		    boolean cups_client =
			PackageSystem::Installed ("cups-libs")
			&& ! PackageSystem::Installed ("cups");
		    if (cups_client)
			return 512 + 2;
		    else
			return 256 + 2;
		}
	    }
	if (!cups_exists && lpd_exists)
	    {
		if (lpd_complete)
		    return 1;
		else
		    return 256 + 1;
	    }
	if (cups_exists && lpd_exists)
	    {
		return 3;
	    }
	return 0;
    }

    /**
     * Uninstalls print spoolers and installs selected one.
     * Stops all spoolers before installation.
     * @param to Spooler to install. Should be one of "cups" "lprng"
     */
    global define void switchTo (string to) ``{
	y2milestone ("Switching spooler to %1", to);
	PackageSystem::EnsureTargetInit ();
	list<string> del = ["plp", "lprold", "lprng", "cups", "cups-client",
            "cups-drivers", "cups-drivers-cs", "cups-drivers-de",
            "cups-drivers-stp", "lpdfilter", "filters", "foomatic-filters",
	    "manufacturer-PPDs" ];
	list<string> inst = [];

	list<string> inst_cups = [ "cups", "cups-drivers", "cups-client",
	    "cups-drivers-stp", "filters", "foomatic-filters",
	    "manufacturer-PPDs"];
	list<string> inst_cups_client = [ "cups-client"];
	list<string> inst_other = [ to, "filters", "lpdfilter", "foomatic-filters",
	    "cups-drivers"];

	if ("cups" == to)
        {
            inst = inst_cups;
        }
        else if ("cups_client" == to)
        {
            inst = inst_cups_client;
	    del = filter (string d, del, ``(! contains (inst_cups, d)));
        }
        else
        {
	    // after switch from CUPS to LPRng printcap with imported queues
	    // can be left. remove it (#29440)
	    y2milestone ("Removing /etc/printcap");
	    SCR::Execute (.target.bash,
		"test -f /etc/printcap && /bin/rm /etc/printcap");
	    inst = inst_other;
        }
	del = filter (string e, del, ``(! contains (inst, e)));


	if (Mode::config)
	{
	    Spooler::Set (to);
	    PackageAI::RemoveAll (del);
	    PackageAI::InstallAll (inst);
	    if (Spooler::Get () == "cups")
	    {
		// using the fact that if I mark package for deletion, it gets
		// deleted during the PackageSystem::InstallAll () call.
		PackageSystem::EnsureTargetInit ();
		PackageSystem::EnsureSourceInit ();
		foreach (string p, del, {Pkg::PkgDelete (p);});
		PackageSystem::InstallAll (inst);
		foreach (string p, del, {Pkg::PkgNeutral (p);});
	    }
	    return;
	}
	StopServices ();
	Service::Disable ("lpd");
	Service::Disable ("cups");
	del = filter (string e, del, ``(Pkg::IsProvided (e) == true));
	inst = filter (string i, inst, ``(! Pkg::IsProvided (i)));
	if (! (Pkg::IsProvided ("ghostscript-x11")
	    || Pkg::IsProvided ("ghostscript-serv")))
	{
	    inst = add (inst, "ghostscript-x11");
	}
	if (del != [] || inst != [])
	{
	    import "PackageCallbacksInit";
	    PackageCallbacksInit::InitPackageCallbacks ();
	    y2milestone("-------- deleting = %1", del);
	    foreach (string e, del, ``{Pkg::PkgDelete (e);});
	    y2milestone("-------- installing = %1", inst);
	    foreach (string e, inst, ``{Pkg::PkgInstall (e);});
	    Pkg::PkgSolve (false);
	    Pkg::PkgCommit (0);
	    PackageSystem::RunSUSEconfig ();
	}
	if (to == "cups" || to == "cups_client")
	{
	    // after switch from CUPS to LPRng printcap with imported queues
	    // can be left. remove it (#29440)
	    // deleting when switching to CUPS doesn't break anything
	    y2milestone ("Removing /etc/printcap");
	    SCR::Execute (.target.bash,
		"test -f /etc/printcap && /bin/rm /etc/printcap");
	}
	if ("cups" == to)
        {
	    // create symlink /etc/printcap -> /etc/cups/printcap (#29671)
	    y2milestone ("Creating /etc/printcap symlink");
	    SCR::Execute (.target.bash,
		"/bin/ln -sf /etc/cups/printcap /etc/printcap");
	}

	sys_forbidden_names = nil;
    }

    /**
     * Check if drivers for current spooler are installed
     * @return boolean true if all available drivers are installed
     */
    global define boolean AreDriversInstaller () ``{
	if (are_drivers_installed != nil)
	    return are_drivers_installed;
	list<string> packages = [];
	if (spooler == "cups")
	{
	    packages = ["cups-drivers", "cups-drivers-stp", "filters",
		"foomatic-filters", "manufacturer-PPDs"];
	}
	else
	{
	    packages = ["lpdfilter", "filters", "cups-drivers",
		"foomatic-filters"];
	}
	packages = filter (string p, packages, ``(
	    available_packages[p]:true
	));
	y2milestone ("Packages to check for installing: %1", packages);
	if (Package::InstalledAll (packages))
	{
	    are_drivers_installed = true;
	}
	else
	{
	    packages = filter (string p, packages, ``{
		boolean available = Package::Available (p);
		available_packages[p] = available;
		return available;
	    });
	    if (Package::InstalledAll (packages))
		are_drivers_installed = true;
	    else
		are_drivers_installed = false;
	}
	return are_drivers_installed;
    }

    /**
     * Install drivers for current spooler
     * @return boolean true on success
     */
    global define boolean InstallDrivers () ``{
	boolean i_result = true;
	list<string> packages = [];
	if (spooler == "cups")
	{
	    packages = ["cups-drivers", "cups-drivers-stp", "filters",
		"foomatic-filters", "manufacturer-PPDs"];
	}
	else
	{
	    packages = ["lpdfilter", "filters", "cups-drivers",
		"foomatic-filters"];
	}
	packages = filter (string p, packages, ``(
	    available_packages[p]:true
	));
	y2milestone ("Packages to check for installing: %1", packages);
	if (! Package::InstalledAll (packages))
	{
	    packages = filter (string p, packages, ``{
		boolean available = Package::Available (p);
		available_packages[p] = available;
		return available;
	    });
	    y2milestone ("Available packages: %1", packages);
	    i_result = Package::InstallAllMsg (packages,
		// question popup, %1 is list of package names
		_("Printer drivers are not installed. YaST will install
packages %1."));
	    if (! (i_result || Package::LastOperationCanceled ()))
	    {
		// error report
		Report::Error (_("Installing packages failed."));
	    }
	}
	return i_result;
    }

    /**
      * check whether spooler (if needed to be running) runns, if not start it
      * @param showPopup boolean true if shall show a popup if needed
      */
    global define void RestartIfNeeded (boolean showPopup) ``{
	if (Mode::config)
	    return;
        if (Spooler::Get () == "cups")
        {
            if (`server == CUPS::InstallationType ())
            {
		boolean close_dialog = false;
		integer status = (integer)SCR::Execute (.target.bash, "/usr/bin/lpq 2>/dev/null");
                if (status == 0)
                    return;
		if (SCR::Read (.target.size, "/etc/cups/ppds.dat") <= 0)
		{
		    close_dialog = true;
		    showCupsInitPopup ();
		}
                status = (integer)SCR::Execute (.target.bash, "/etc/init.d/cups status");
                if (0 != status)
		{
                    SCR::Execute (.target.bash_background, "/etc/init.d/cups start");
		    sleep (1000);
		}
                status = 1;
		integer attempts = 0;
                while (true)
                {
                    status = (integer)SCR::Execute (.target.bash, "/usr/bin/lpq 2>/dev/null");
                    if (status == 0)
                        break;
		    attempts = attempts + 1;
		    if (attempts > 5 && ! close_dialog)
		    {
			close_dialog = true;
			showCupsInitPopup ();
		    }
		    if (attempts>60*10)
		    {
			break;
		    }
                    if (close_dialog)
                        UI::ChangeWidget (`id (`cups_prog), `Value, attempts);
                    sleep (1000);
                    status = (integer)SCR::Execute (.target.bash, "/etc/init.d/cups status");
                    if (0 != status)
                    {
                        Popup::Error (
                            // error popup
                            _("CUPS daemon died during initialization"));
			break;
                    }
                }
		if (close_dialog)
		{
		    UI::CloseDialog ();
		}
            }
        }
    }

    /**
      * Adjust services for runlevel, set them to state needed for writing
      * @param have_some_queue true if at least one queue was configured
      * @return boolean true on success
      */
    global define boolean AdjustServices (boolean have_some_queue) ``{
	// FIXME error checking
	StopServices();

	if (Spooler::Get () == "cups")
	{
	    if ((boolean)SCR::Read (.init.scripts.exists, "lpd"))
		Service::Disable ("lpd");
            if (`client == CUPS::InstallationType ())
	    {
		if ((boolean)SCR::Read (.init.scripts.exists, "cups"))
		    Service::Disable ("cups");
	    }
	    else
	    {
		Service::Enable ("cups");
	    }
	}
	else
	{
	    if ((boolean)SCR::Read (.init.scripts.exists, "cups"))
		Service::Disable ("cups");
	    if ((boolean)SCR::Read (.init.scripts.exists, "lpd") && have_some_queue)
		Service::Enable ("lpd");
	}
	Spooler::RestartIfNeeded (true);
	return true;
    }

    /**
     * Check what spool system is installed. Don't ask user.
     * @return string spooler
     */
    global define string checkSpoolSystemNoDialog () ``{
	integer spool_sys = Spooler::getSpoolSystem ();
	spool_sys = spool_sys & 0xFF;
	if (1 == spool_sys) //lpd is installed
	{
	    spooler = "lprng";
	}
	else if (2 == spool_sys) //cups is installed
	{
	    spooler = "cups";
	}
	else
	{
	    spooler = "unknown";
	}
	return spooler;
    }

    /**
     * Check what spool system is installed. Ask user if needed and install spooler if wanted.
     * @return string spooler
     */
    global define string checkSpoolSystem () ``{
	integer spool_sys = Spooler::getSpoolSystem ();
	if (1 == spool_sys) //lpd is installed
	{
	    spooler = "lprng";
	    return "lprng";
	}
	else if (2 == spool_sys) //cups is installed
	{
	    spooler = "cups";
	    return "cups";
	}
	else if (0 == spool_sys || 3 == spool_sys || 256 + 1 == spool_sys
	    || 256 + 2 == spool_sys)
	{
	    string message = "";
	    if (0 == spool_sys) //none installed
	    {
		// popup message
		message = _("No printing system installed. 
Select the spooler to 
install and configure.
");
	    }
	    else if (3 == spool_sys) //both spoolers are installed
	    {
		// popup message
	message = _("Both LPRng and CUPS are installed.
Select the spooler  
to leave installed and configure.");
	    }
	    else if (256 + 1 == spool_sys) // lpd is installed, but seems
					   // to be not complete
	    {
		// popup message
		message = _("An LPD-compatible spooler, probably LPRng,
is installed, but the installation does not seem 
complete. Choose to reinstall 
LPRng, install CUPS, or leave the current status.
To repair the current configuration, select LPRng.
If you leave the current status, your spooler may not
run properly.
");
	    }
	    else if (256 + 2 == spool_sys) // cups is installed, but seems
					   // to be not complete
	    {
		// popup message
		message =_("The CUPS spooler is installed, but the installation 
does not seem complete. Choose to reinstall
CUPS, install LPRng, or leave the current status. 
To repair the current configuration, select the 
CUPS full server installation.
If you leave the current status, your spooler 
may not run properly.");
	    }
	    symbol result = chooseSpooler (message,
		// pushbutton
		_("Abo&rt setup"));
	    if (`cups_server == result)
	    {
		Spooler::switchTo ("cups");
		spooler = "cups";
		return "cups";
	    }
	    else if (`cups_client == result)
            {
                Spooler::switchTo ("cups_client");
		spooler = "cups";
                return "cups";
            }
	    else if (`lprng == result)
	    {
		Spooler::switchTo ("lprng");
		spooler = "lprng";
		return "lprng";
	    }
            else if (`cancel == result)
                return "exit";
	    else
		return "unknown";
	}
	else if (512 + 2 == spool_sys) // only client installation of cups
				       // is installed
	{
	    spooler = "cups";
	    return "cups";
	}
	return ""; //should be never reached
    }

    /**
      * Test whether spooler supports queue type
      * @param queue string queue type (eg. parallel)
      * @param spooler string "cups" or "lprng"
      * @return boolean true if supports
      */
    global define boolean testSupportedQueueBySpooler
	(string queue, string spooler)
    ``{
	map supported = $[ "cups" : [ "parallel", "usb", "serial", "lpd", "ipp", "samba", "socket", "irda", "class", "novell", "pipe", "file", ],
			   "lprng" : [ "parallel", "usb", "serial", "lpd", "samba", "novell", "irda", "socket", "pipe", ]];
	list l = lookup (supported, spooler, []);
	if (l == [])
	    return true;
	if (contains (l, queue))
	    return true;
	if (spooler == "cups")
	{
	    list<string> backends
		= (list<string>)SCR::Read (.target.dir, "/usr/lib/cups/backend");
	    if (backends != nil)
		return contains (backends, queue);
	}
	return false;
    }
    /**
      * Test whether currently used spooler supports queue type
      * @param queue string queue type (eg. parallel)
      * @return boolean true if supports
      */
    global define boolean testSupportedQueue (string queue) ``{
	return testSupportedQueueBySpooler (queue, Spooler::Get ());
    }

    /**
      * Restart printer daemon
      * @param have_some_queue true if at least one queue was configured
      * @return boolean true on success
      */
    global define boolean restartServices (boolean have_some_queue) ``{
	Spooler::StopServices ();
// always adjust CUPS to be run (bugzilla 29413)
	if (have_some_queue || (Spooler::Get () == "cups"))
	{
	    if (Spooler::Get () == "cups")
		SCR::Execute (.target.bash,
		    "test -f /etc/init.d/cups && /etc/init.d/cups start");
	    else
		SCR::Execute (.target.bash,
		    "test -f /etc/init.d/lpd && /etc/init.d/lpd start");
	}
	return true;
    }

    /**
      * List all available queues (local and remote)
      * @return a list of strings all available queues
      */
    global define list<string> GetAvailableQueues () ``{
	if (Spooler::Get () == "cups")
	{
	    return CUPS::GetAvailableQueues ();
	}
	else
	return LPRng::GetAvailableQueues ();
    }



}
