/**
 * File:
 *  printerdb.ycp
 * Summary:
 *  Printer database viewer
 * Authors:
 *  Petr Blahos <pblahos@suse.cz>
 *
 * $Id$
 */
{
    import "Wizard";
    textdomain "printer";

    include "wizard/sequencer.ycp";

    string current_vendor = "";
    string current_printer = "";
    string current_config = "";

    // strings which may be useable for 8.1, but currently not used
//    string a = _("Ready");
//    a = _("Status");
//    a = _("&Baud rate:");
//    a = _("&Parity:");
//    a = _("&Data bits:");
//    a = _("&Flow control:");
//    a = _("None");
//    a = _("Even");
/*    a = _("Odd");
    a = _("None");
    a = _("XON/XOFF (Software)");
    a = _("RTS/CTS (Hardware)");
    a = _("DTR/DSR (Hardware)");
*/
    define getConfigTreeHelper (list l, string pre) ``{
	list ll = [];
	foreach (`i, l, ``{
		ll = add (ll, `item (`id (lookup (i, "ident", "")), haskey (i, "sub") ? "" : "x", pre + lookup (i, "text", ""), lookup (i, "ident", ""), lookup (i, "queue", "")));
		if (haskey (i, "sub")) {
		    list x = getConfigTreeHelper (lookup (i, "sub", []), "    " + pre);
		    foreach (`j, x, ``{
			ll = add (ll, j);
		    });
		}
	    });
	return ll;
    }

    define getConfigTree () ``{
	list l = SCR::Read (add (.printerdb.configstree, current_printer));
	return getConfigTreeHelper (l, "");
    }

    define vendors () ``{
	list items = SCR::Read (.printerdb.vendors, current_vendor);
	Wizard::SetContentsButtons
	// Dialog caption
/*	UI::SetWizardContents i*/(_("Vendors"), `VBox (
		    `SelectionBox (`id (`sel), "", items),
			// pushbutton
		    `PushButton (`id (`generate), _("&Generate translation strings and test script"))
		    ), "", BackButtonLabel (), NextButtonLabel ());
	symbol ret = UI::UserInput ();
	while (`generate == ret)
	{
	    string tmp = SCR::Read (.target.tmpdir) + "/printerdb.";
	    SCR::Write (.printerdb.gettext.db, tmp + "db.msg");
	    SCR::Write (.printerdb.gettext.prg, tmp + "prg.msg");
	    SCR::Write (.printerdb.testscript, tmp + "tst");
	    UI::OpenDialog (`VBox (
			// label
			`Label (sformat (_("Files %1msg and %1tst were generated."), tmp)),
			`HBox (`PushButton (OKButtonLabel ()),`PushButton (CancelButtonLabel ()))));
	    ret = UI::UserInput ();
	    UI::CloseDialog ();
	    ret = UI::UserInput ();
	}
	current_vendor = select (UI::QueryWidget (`id (`sel), `CurrentItem), 0, "");
	return ret;
    }

    define printers () ``{
	list items = SCR::Read (add (.printerdb.models, current_vendor), current_printer);
	Wizard::SetContentsButtons
	// dialog caption
/*	UI::SetWizardContents*/ (_("Printers"), `VBox (
		    `SelectionBox (`id (`sel), "", items)
		    ), "", BackButtonLabel (), NextButtonLabel ());
	symbol ret = UI::UserInput ();
	current_printer = select (UI::QueryWidget (`id (`sel), `CurrentItem), 0, "");
	return ret;
    }

    define configsrt () ``{
	list l = SCR::Read (add (.printerdb.configsrt, current_printer), "");
	Wizard::SetContentsButtons
	// dialog caption
/*	UI::SetWizardContents*/ (_("Configs"), `VBox (
		    `RichText (`id (`sel), select (l, 0, "")),
			// menubutton
		    `MenuButton (_("Configurations"), select (l, 1, []))
		    ),
		    "",  BackButtonLabel (), NextButtonLabel ());
	any ret = "";
	while (!is (ret, symbol))
	{
	    ret = UI::UserInput ();
	    if (is (ret, string))
	    {
		current_config = ret;
		ret = `next;
	    }
	}
	return ret;
    }
    define configs () ``{
	Wizard::SetContentsButtons
	// dialog caption
/*	UI::SetWizardContents */(_("Configs"), `VBox (
		    `Table (`id (`sel), `header ("leaf","name", "id", "queue"), getConfigTree ())
		    ), "", BackButtonLabel (), NextButtonLabel ());
	symbol ret = UI::UserInput ();
	current_config = UI::QueryWidget (`id (`sel), `CurrentItem);
	return ret;
    }
    define options () ``{
	Wizard::SetContentsButtons
	// dialog caption
/*	UI::SetWizardContents */(_("Options"), `VBox (
			//selectionbox label
		    `VWeight (1, `ReplacePoint (`id (`upper), `SelectionBox (`id (`opts), _("&Options"), []))),
			//selectionbox label
		    `VWeight (1, `ReplacePoint (`id (`lower), `SelectionBox (`id (`vals), _("&Values"), [])))
		    ), "", BackButtonLabel (), NextButtonLabel ());
	list opts = SCR::Read (add (.printerdb.options, current_config));
			//selectionbox label
	UI::ReplaceWidget (`id (`upper), `SelectionBox (`id (`opts), `opt (`notify), _("&Options"), opts));
	symbol ret = `opts;
	map selected = $[];
	while (true) {
	    if (`next == ret || `back == ret || `abort == ret)
		break;
	    if (`opts == ret) {
		string sel = UI::QueryWidget (`id (`opts), `CurrentItem);
		any vals = SCR::Read (add (.printerdb.values, sel));
		y2error ("====> %2 %1", getValuesUI (vals), vals);
		UI::ReplaceWidget (`id (`lower), getValuesUI (vals));
	    }
	    else if (`vals == ret) {
		string co = UI::QueryWidget (`id (`opts), `CurrentItem);
		selected = add (selected, co, UI::QueryWidget (`id (`vals), `CurrentItem));
		opts = SCR::Read (add (add (.printerdb.options, current_config), co), selected);
		// selectionbox
		UI::ReplaceWidget (`id (`upper), `SelectionBox (`id (`opts), `opt (`notify), _("&Options"), opts));
	    }
	    ret = UI::UserInput ();
	}
	return ret;
    }

    define getValuesUI (any vals) ``{
	if (is (vals,list))
		// selectionbox
	    return `SelectionBox (`id (`vals), `opt (`notify), _("&Values"), vals);
	if (!is (vals, term))
	    return `VStretch ();
	if (`int == symbolof (vals))
	{
	    // intfield
	    term out = UI::HasSpecialWidget (`Slider) ? `Slider (`id (`int), _("&Values")) : `IntField (`id (`int), _("&Values"));
	    out = add (out, select (vals, 0, 0));
	    out = add (out, select (vals, 1, 0));
	    out = add (out, select (vals, 2, 0));
	    out = `VBox (
		    // label, %1-3 replaced by integers
		    `Label (`opt (`hstretch), sformat (_("Minimum value: %1, Maximum value: %2, Step: %3"), select (vals, 0, nil), select (vals, 1, nil), select (vals, 2, nil))),
		    out);
	    return out;
	}
	if (`float == symbolof (vals))
	{
	    // textentry
	    term out = `TextEntry (`id (`float), _("&Values"), sformat ("%1",select (vals,2,nil)));
	    out = `VBox (
		    // label, %1-3 replaced by integers
		    `Label (`opt (`hstretch), sformat (_("Minimum value: %1, Maximum value: %2, Step: %3"), select (vals, 0, nil), select (vals, 1, nil), select (vals, 2, nil))),
		    out);
	    return out;
	}
	return `VStretch ();
    }

    Wizard::CreateDialog ();
    WizardSequencer (
	    $[
	    "vendors" : ``(vendors ()),
	    "printers" : ``(printers ()),
	    "configs" : ``(configs ()),
	    "configs" : ``(configsrt ()),
	    "options" : ``(options ()),
	    ],
	    $[
	    "ws_start" : "vendors",
	    "vendors" : $[
		`next : "printers",
		`abort: `abort,
	    ],
	    "printers" : $[
		`abort: `abort,
		`next: "configs",
	    ],
	    "configs" : $[
		`abort: `abort,
		`next : "options",
	    ],
	    "options" : $[
		`abort : `abort,
		`next : `next,
	    ]
	    ]
	    );

    UI::CloseDialog ();
}

